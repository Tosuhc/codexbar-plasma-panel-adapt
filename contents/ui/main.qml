import QtQuick
import QtQuick.Controls as Controls
import QtQuick.Layouts
import org.kde.kirigami as Kirigami
import org.kde.plasma.components as PlasmaComponents
import org.kde.plasma.core as PlasmaCore
import org.kde.plasma.plasma5support as Plasma5Support
import org.kde.plasma.plasmoid
import "components" as Components
import "ProviderIdentity.js" as ProviderIdentity
import "SafeText.js" as SafeText
import "ThemeContrast.js" as ThemeContrast
import "UsageDetails.js" as UsageDetails
import "UpdateLogic.js" as UpdateLogic

PlasmoidItem {
    id: root

    Plasmoid.icon: "view-statistics"
    Plasmoid.title: "CodexBar"
    toolTipMainText: Plasmoid.title
    toolTipSubText: panelToolTipText()
    toolTipTextFormat: Text.PlainText
    Plasmoid.contextualActions: [
        PlasmaCore.Action {
            text: i18n("Refresh")
            icon.name: "view-refresh"
            onTriggered: root.refreshNow()
        }
    ]

    property string commandPath: (Plasmoid.configuration.commandPath || "codexbar").trim()
    property string provider: (Plasmoid.configuration.provider || "").trim()
    property string source: (Plasmoid.configuration.source || "").trim()
    property int refreshIntervalSec: isFinite(Number(Plasmoid.configuration.refreshInterval)) ? Math.max(0, Number(Plasmoid.configuration.refreshInterval)) : 300
    property bool includeStatus: Plasmoid.configuration.includeStatus
    property bool costUsageEnabled: Plasmoid.configuration.costUsageEnabled !== false
    property int costHistoryDays: isFinite(Number(Plasmoid.configuration.costHistoryDays)) ? Math.max(1, Math.min(365, Number(Plasmoid.configuration.costHistoryDays))) : 30
    property bool usageBarsShowUsed: Plasmoid.configuration.usageBarsShowUsed === true
    property bool showQuotaWarningMarkers: Plasmoid.configuration.showQuotaWarningMarkers !== false
    property bool enableNotifications: Plasmoid.configuration.enableNotifications !== false
    property bool notifyStatusIncidents: Plasmoid.configuration.notifyStatusIncidents !== false
    property bool notifyQuotaWarnings: Plasmoid.configuration.notifyQuotaWarnings !== false
    property bool notifyLimitResets: Plasmoid.configuration.notifyLimitResets !== false
    property bool updateChecksEnabled: Plasmoid.configuration.updateChecksEnabled !== false
    property bool updateNotificationsEnabled: Plasmoid.configuration.updateNotificationsEnabled !== false
    property bool autoUpdateEnabled: Plasmoid.configuration.autoUpdateEnabled === true
    property int autoUpdateIntervalHours: isFinite(Number(Plasmoid.configuration.autoUpdateIntervalHours)) ? Math.max(1, Math.min(168, Number(Plasmoid.configuration.autoUpdateIntervalHours))) : 24
    property string autoUpdateLastCheck: Plasmoid.configuration.autoUpdateLastCheck || ""
    property string menuBarDisplayMode: safeMenuBarDisplayMode(Plasmoid.configuration.menuBarDisplayMode)
    property bool resetTimesShowAbsolute: Plasmoid.configuration.resetTimesShowAbsolute === true
    property bool showProviderChangelogs: Plasmoid.configuration.showProviderChangelogs === true
    property bool autoSelectProvider: Plasmoid.configuration.autoSelectProvider === true
    property string overviewProviderIDsRaw: Plasmoid.configuration.overviewProviderIDs || ""
    readonly property int maxOverviewProviders: 3
    property int providerConfigRevision: boundedConfigRevision(Plasmoid.configuration.providerConfigRevision)
    property var providers: []
    property var providerDisplayNames: ({})
    property string errorText: ""
    property string lastUpdatedText: ""
    property bool loading: false
    property string commandSource: buildCommand()
    property string connectedCommandSource: ""
    property string providerConfigCommandSource: buildProviderConfigCommand()
    property string connectedProviderConfigCommandSource: ""
    property string providerConfigWatchCommand: buildProviderConfigWatchCommand()
    property string providerConfigStamp: ""
    readonly property int providerConfigWatchIntervalMs: 60000
    property int commandRunSerial: 0
    property var activeUsageCommands: ({})
    readonly property int usageCommandTimeoutMs: 120000
    readonly property int maximumExtraRateWindows: 24
    readonly property int maximumProviderSnapshots: 256
    readonly property int maximumAccountSnapshots: 128
    readonly property int maximumCostSnapshots: 256
    readonly property int maximumCostHistoryPoints: 365
    readonly property int maximumCostHistoryScanItems: 2048
    readonly property int maximumModelBreakdownsPerDay: 128
    readonly property int maximumConcurrentProviderFallbackCommands: 8
    property var pendingProviderCommands: ({})
    property var fallbackProviderQueue: []
    property int activeProviderFallbackCount: 0
    property var fallbackProviderOrder: []
    property var fallbackProviderResults: ({})
    property var fallbackProviderSeen: ({})
    property int pendingProviderCount: 0
    property bool providerFallbackActive: false
    property string costCommandSource: buildCostCommand()
    property string connectedCostCommandSource: ""
    property var tokenCosts: ({})
    property string costErrorText: ""
    property string selectedProviderID: ""
    property bool selectionInitialized: false
    property var selectedAccounts: ({})
    property var accountOptions: ({})
    property var accountErrors: ({})
    property var accountLoading: ({})
    property var pendingAccountCommands: ({})
    readonly property int accountCommandTimeoutMs: 60000
    property var notificationMemo: ({})
    property var notificationRefreshPending: ({})
    property bool notificationsPrimed: false
    property string connectedUpdateCommandSource: ""
    readonly property int widgetUpdateCheckTimeoutMs: 60000
    readonly property int widgetAutoUpdateTimeoutMs: 600000
    readonly property int widgetUpdateMinimumTimerDelayMs: 60000
    property string updateStatusText: boundedWidgetUpdateText(Plasmoid.configuration.widgetUpdateLastStatus)
    property string updateErrorText: boundedWidgetUpdateText(Plasmoid.configuration.widgetUpdateLastError)
    property string lastNotifiedUpdateVersion: Plasmoid.configuration.lastNotifiedUpdateVersion || ""
    readonly property bool verticalFormFactor: Plasmoid.formFactor === PlasmaCore.Types.Vertical
    readonly property var overviewProviderItems: overviewProviders()
    readonly property bool overviewAvailable: provider.length === 0 && providers.length > 1 && overviewProviderItems.length > 0
    readonly property int selectedProviderIndex: providerIndexForID(selectedProviderID)
    readonly property bool overviewSelected: overviewAvailable && selectionInitialized && selectedProviderID.length === 0
    readonly property var selectedProviderData: providers.length > 0 && selectedProviderIndex >= 0
        ? providers[Math.min(selectedProviderIndex, providers.length - 1)]
        : null
    readonly property real roundedSurfaceRadius: Kirigami.Units.cornerRadius
        + Kirigami.Units.smallSpacing
    // Radius for a small surface drawn inside a roundedSurfaceRadius container.
    // Concentric rounding wants the inner radius reduced by the inset between
    // the two edges, and popup surfaces inset their content by smallSpacing, so
    // this is roundedSurfaceRadius minus that inset by construction.
    readonly property real nestedSurfaceRadius: Kirigami.Units.cornerRadius
    // Two-step de-emphasis scale for popup text. A supporting label uses the
    // secondary step and the value it annotates uses the stronger step, so a
    // label/value pair keeps its hierarchy without inventing a new opacity per
    // section. 0.7 is the lowest step where Kirigami.Theme.textColor still
    // clears WCAG AA 4.5:1 against the Breeze Light popup background.
    readonly property real secondaryTextOpacity: 0.7
    readonly property real valueTextOpacity: 0.85
    // Shared track height for the credits, cost, and usage meters that stack in
    // one provider detail view. Derived from gridUnit so it follows the user
    // font size instead of pinning a device pixel count.
    readonly property real meterTrackHeight: Math.round(Kirigami.Units.gridUnit * 0.4)
    // Thinner track for meters that sit inside a scannable list row (overview
    // rows, cost history rows) instead of leading a detail section. Keeping the
    // two scales apart is what makes the primary meters read as primary.
    readonly property real compactMeterTrackHeight: Math.round(Kirigami.Units.gridUnit * 0.28)

    onCommandSourceChanged: Qt.callLater(refreshNow)
    onCostUsageEnabledChanged: Qt.callLater(refreshCost)
    onCostHistoryDaysChanged: Qt.callLater(refreshCost)
    onProviderConfigRevisionChanged: Qt.callLater(refreshNow)
    onAutoSelectProviderChanged: updateSelectedProvider()
    onOverviewProviderIDsRawChanged: updateSelectedProvider()
    onEnableNotificationsChanged: resetNotificationMemo()
    onNotifyStatusIncidentsChanged: resetNotificationMemo()
    onNotifyQuotaWarningsChanged: resetNotificationMemo()
    onNotifyLimitResetsChanged: resetNotificationMemo()
    onUpdateChecksEnabledChanged: {
        if (updateChecksEnabled) {
            Qt.callLater(function() { root.checkForWidgetUpdate(true) })
        } else {
            updateCheckTimer.stop()
        }
    }
    onAutoUpdateIntervalHoursChanged: scheduleNextUpdateCheck()
    onAutoUpdateEnabledChanged: {
        if (updateChecksEnabled && autoUpdateEnabled) {
            Qt.callLater(function() { root.checkForWidgetUpdate(true) })
        }
    }
    onProvidersChanged: {
        if (providers.length === 0) {
            selectedProviderID = ""
            selectionInitialized = false
            resetNotificationMemo()
            return
        }
        updateSelectedProvider()
        Qt.callLater(processNotifications)
    }

    Component.onCompleted: {
        if (providerConfigWatchCommand.length > 0) {
            providerConfigWatcher.connectSource(providerConfigWatchCommand)
        }
        refreshNow()
        if (updateChecksEnabled) {
            scheduleNextUpdateCheck()
            Qt.callLater(checkForWidgetUpdate)
        }
    }

    function buildCommand() {
        if (commandPath.length === 0) {
            return ""
        }

        var parts = [
            shellQuote(commandPath),
            "usage",
            "--format",
            "json",
            "--json-only"
        ]

        if (provider.length > 0) {
            parts.push("--provider")
            parts.push(shellQuote(provider))
            var selectedAccount = selectedAccountForProvider(provider)
            if (selectedAccount.length > 0) {
                parts.push("--account")
                parts.push(shellQuote(selectedAccount))
            }
        }

        if (source.length > 0) {
            parts.push("--source")
            parts.push(shellQuote(source))
        }

        if (includeStatus) {
            parts.push("--status")
        }

        return parts.join(" ")
    }

    function buildProviderAccountsCommand(providerID) {
        if (commandPath.length === 0) {
            return ""
        }

        var parts = [
            shellQuote(commandPath),
            "usage",
            "--provider",
            shellQuote(providerCliArgument(providerID)),
            "--all-accounts",
            "--format",
            "json",
            "--json-only"
        ]

        if (source.length > 0) {
            parts.push("--source")
            parts.push(shellQuote(source))
        }

        if (includeStatus) {
            parts.push("--status")
        }

        return parts.join(" ")
    }

    function buildProviderConfigCommand() {
        if (commandPath.length === 0) {
            return ""
        }

        return [
            shellQuote(commandPath),
            "config",
            "providers",
            "--format",
            "json",
            "--json-only"
        ].join(" ")
    }

    function buildProviderConfigWatchCommand() {
        var script = [
            "config=${CODEXBAR_CONFIG:-};",
            "case \"$config\" in '~/'*) config=\"$HOME/${config#\\~/}\";; esac;",
            "if [ -z \"$config\" ]; then",
            "xdg=${XDG_CONFIG_HOME:-};",
            "case \"$xdg\" in '~/'*) xdg=\"$HOME/${xdg#\\~/}\";; esac;",
            "case \"$xdg\" in",
            "/*) config=\"$xdg/codexbar/config.json\";;",
            "*) config=\"$HOME/.config/codexbar/config.json\"; if [ ! -e \"$config\" ] && [ -e \"$HOME/.codexbar/config.json\" ]; then config=\"$HOME/.codexbar/config.json\"; fi;;",
            "esac;",
            "fi;",
            "if [ -r \"$config\" ]; then cksum \"$config\"; else printf missing; fi"
        ].join(" ")
        return ["sh", "-c", shellQuote(script)].join(" ")
    }

    function buildProviderUsageCommand(providerID) {
        var parts = [
            shellQuote(commandPath),
            "usage",
            "--provider",
            shellQuote(providerCliArgument(providerID)),
            "--format",
            "json",
            "--json-only"
        ]

        if (source.length > 0) {
            parts.push("--source")
            parts.push(shellQuote(source))
        }

        var selectedAccount = selectedAccountForProvider(providerID)
        if (selectedAccount.length > 0) {
            parts.push("--account")
            parts.push(shellQuote(selectedAccount))
        }

        if (includeStatus) {
            parts.push("--status")
        }

        return parts.join(" ")
    }

    function buildCostCommand() {
        if (commandPath.length === 0) {
            return ""
        }
        if (!costUsageEnabled) {
            return ""
        }

        var parts = [
            shellQuote(commandPath),
            "cost",
            "--format",
            "json",
            "--json-only",
            "--days",
            String(costHistoryDays)
        ]

        if (provider.length > 0) {
            parts.push("--provider")
            parts.push(shellQuote(provider))
        }

        return parts.join(" ")
    }

    function shellQuote(value) {
        return "'" + String(value).replace(/'/g, "'\\''") + "'"
    }

    function safeMenuBarDisplayMode(value) {
        var mode = String(value || "percent")
        if (mode === "percent" || mode === "pace" || mode === "both" || mode === "resetTime") {
            return mode
        }
        return "percent"
    }

    function boundedConfigRevision(value) {
        var revision = Number(value)
        if (!isFinite(revision)) {
            return 0
        }
        return Math.max(0, Math.min(2147480000, Math.floor(revision)))
    }

    function boundedDisplayText(value, maximumLength) {
        var limit = Number(maximumLength)
        if (!isFinite(limit) || limit <= 0) {
            limit = 500
        }
        limit = Math.min(2000, Math.floor(limit))
        return SafeText.boundedDisplayText(value, limit)
    }

    function boundedWidgetUpdateText(value) {
        return boundedDisplayText(value, 500)
    }

    function boundedCliMessage(value) {
        return SafeText.cliMessage(value, SafeText.maximumCliMessageLength)
    }

    function isCliRecord(value) {
        return value !== null && typeof value === "object" && !Array.isArray(value)
    }

    function normalizedProviderID(value) {
        if (typeof value !== "string") {
            return ""
        }
        var trimmed = value.trim()
        if (trimmed.length === 0 || trimmed.length > ProviderIdentity.maximumProviderIDLength) {
            return ""
        }
        return providerMapKey(trimmed)
    }

    function hasOwnKey(item, key) {
        return item ? Object.prototype.hasOwnProperty.call(item, key) : false
    }

    function isUnsafeObjectKey(key) {
        var value = String(key || "")
        return value === "__proto__" || value === "prototype" || value === "constructor"
    }

    function providerMapKey(providerID) {
        var key = providerKey(providerID)
        return ProviderIdentity.providerMapKey(key)
    }

    function commandWithRunNonce(command) {
        if (command.length === 0) {
            return ""
        }
        commandRunSerial += 1
        return "CODEXBAR_PLASMA_RUN=" + commandRunSerial + " " + command
    }

    function connectUsageCommand(sourceName, descriptor) {
        if (sourceName.length === 0) {
            return
        }

        var commands = copyObject(activeUsageCommands)
        commands[sourceName] = descriptor || {
            kind: "",
            providerID: "",
            deadlineMs: 0
        }
        activeUsageCommands = commands
        usageSource.connectSource(sourceName)
    }

    function buildUsageCommandDescriptor(kind, providerID) {
        return {
            kind: String(kind || ""),
            providerID: String(providerID || ""),
            deadlineMs: Date.now() + usageCommandTimeoutMs
        }
    }

    function finishUsageCommandSource(sourceName) {
        if (sourceName.length === 0) {
            return
        }

        usageSource.disconnectSource(sourceName)

        var activeCommands = copyObject(activeUsageCommands)
        delete activeCommands[sourceName]
        activeUsageCommands = activeCommands
    }

    function refreshNow() {
        retireUsageCommands()
        retireStaleAccountCommands()
        refreshCost()
        providerFallbackActive = false

        if (commandSource.length === 0) {
            loading = false
            errorText = i18n("Set the codexbar command path in widget settings.")
            return
        }

        loading = true
        errorText = ""
        if (canUseProviderFallback()) {
            startProviderFallback()
            return
        }
        connectedCommandSource = commandWithRunNonce(commandSource)
        connectUsageCommand(
            connectedCommandSource,
            buildUsageCommandDescriptor("usage", ""))
    }

    function retireUsageCommands() {
        if (connectedCommandSource.length > 0) {
            finishUsageCommandSource(connectedCommandSource)
            connectedCommandSource = ""
        }
        if (connectedProviderConfigCommandSource.length > 0) {
            finishUsageCommandSource(connectedProviderConfigCommandSource)
            connectedProviderConfigCommandSource = ""
        }
        for (var command in pendingProviderCommands) {
            finishUsageCommandSource(command)
        }
        // Account loads are user-triggered; keep them alive across usage refreshes
        // so their replies can still populate the account picker.
        pendingProviderCommands = ({})
        fallbackProviderQueue = []
        activeProviderFallbackCount = 0
        fallbackProviderOrder = []
        fallbackProviderResults = ({})
        fallbackProviderSeen = ({})
        pendingProviderCount = 0
    }

    function handleProviderConfigWatch(stdoutText) {
        var stamp = stdoutText.trim()
        if (stamp.length === 0) {
            return
        }
        if (providerConfigStamp.length === 0) {
            providerConfigStamp = stamp
            return
        }
        if (stamp === providerConfigStamp) {
            return
        }
        providerConfigStamp = stamp
        Qt.callLater(refreshNow)
    }

    function refreshCost() {
        if (connectedCostCommandSource.length > 0) {
            finishUsageCommandSource(connectedCostCommandSource)
            connectedCostCommandSource = ""
        }

        if (costCommandSource.length === 0) {
            tokenCosts = ({})
            costErrorText = ""
            applyTokenCosts()
            return
        }

        costErrorText = ""
        connectedCostCommandSource = commandWithRunNonce(costCommandSource)
        connectUsageCommand(
            connectedCostCommandSource,
            buildUsageCommandDescriptor("cost", ""))
    }

    function parseOutput(stdoutText, stderrText) {
        var trimmed = stdoutText.trim()
        if (trimmed.length === 0) {
            if (canUseProviderFallback()) {
                startProviderFallback()
                return
            }
            providers = []
            errorText = stderrText.trim().length > 0 ? boundedCliMessage(stderrText) : i18n("codexbar did not return JSON.")
            loading = false
            return
        }

        var payload
        try {
            payload = JSON.parse(trimmed)
        } catch (error) {
            providers = []
            errorText = i18n("Could not parse codexbar JSON: %1", error.message)
            loading = false
            return
        }

        var items = Array.isArray(payload) ? payload : [payload]
        var nextProviders = []
        var itemLimit = Math.min(items.length, maximumProviderSnapshots)
        for (var i = 0; i < itemLimit; i++) {
            if (!isCliRecord(items[i]) || normalizedProviderID(items[i].provider).length === 0) {
                continue
            }
            nextProviders.push(normalizeProvider(items[i]))
        }

        markNotificationProvidersFresh(nextProviders)
        providers = nextProviders
        errorText = nextProviders.length === 0 ? boundedCliMessage(stderrText) : ""
        lastUpdatedText = i18n("Updated %1", Qt.formatDateTime(new Date(), "hh:mm"))
        loading = false
    }

    function hasSelectedAccountOverrides() {
        for (var providerID in selectedAccounts) {
            if (hasOwnKey(selectedAccounts, providerID)
                && String(selectedAccounts[providerID] || "").length > 0) {
                return true
            }
        }
        return false
    }

    function canUseProviderFallback() {
        return source.length === 0 || hasSelectedAccountOverrides()
    }

    function startProviderFallback() {
        providerFallbackActive = true
        if (connectedCommandSource.length > 0) {
            finishUsageCommandSource(connectedCommandSource)
            connectedCommandSource = ""
        }
        if (provider.length > 0) {
            startProviderFallbackForProviders([providerKey(provider)])
            return
        }

        if (providerConfigCommandSource.length === 0) {
            providers = []
            errorText = i18n("codexbar did not return JSON.")
            loading = false
            return
        }

        connectedProviderConfigCommandSource = commandWithRunNonce(providerConfigCommandSource)
        connectUsageCommand(
            connectedProviderConfigCommandSource,
            buildUsageCommandDescriptor("providerConfig", ""))
    }

    function parseProviderConfigOutput(stdoutText, stderrText) {
        var trimmed = stdoutText.trim()
        if (trimmed.length === 0) {
            providers = []
            errorText = stderrText.trim().length > 0 ? boundedCliMessage(stderrText) : i18n("Could not load CodexBar provider configuration.")
            loading = false
            return
        }

        var payload
        try {
            payload = JSON.parse(trimmed)
        } catch (error) {
            providers = []
            errorText = i18n("Could not parse CodexBar provider configuration: %1", error.message)
            loading = false
            return
        }

        var providerIDs = []
        var displayNames = ({})
        var items = Array.isArray(payload) ? payload : [payload]
        var itemLimit = Math.min(items.length, maximumProviderSnapshots)
        var seenProviderIDs = ({})
        for (var i = 0; i < itemLimit; i++) {
            if (isCliRecord(items[i]) && items[i].provider) {
                var providerID = normalizedProviderID(items[i].provider)
                if (providerID.length === 0) {
                    continue
                }
                var displayName = boundedDisplayText(items[i].displayName, 120)
                if (displayName.length > 0) {
                    displayNames[providerID] = displayName
                }
                if (items[i].enabled === true && !hasOwnKey(seenProviderIDs, providerID)) {
                    seenProviderIDs[providerID] = true
                    providerIDs.push(providerID)
                }
            }
        }

        providerDisplayNames = displayNames
        startProviderFallbackForProviders(providerIDs)
    }

    function startProviderFallbackForProviders(providerIDs) {
        for (var existingCommand in pendingProviderCommands) {
            finishUsageCommandSource(existingCommand)
        }
        pendingProviderCommands = ({})
        fallbackProviderQueue = []
        activeProviderFallbackCount = 0
        fallbackProviderOrder = []
        fallbackProviderResults = ({})
        fallbackProviderSeen = ({})
        pendingProviderCount = 0

        var seenCommands = ({})
        var commands = ({})
        var commandList = []
        var providerLimit = Math.min(providerIDs.length, maximumProviderSnapshots)
        for (var i = 0; i < providerLimit; i++) {
            var providerID = normalizedProviderID(String(providerIDs[i] || ""))
            if (providerID.length === 0) {
                continue
            }
            var baseCommand = buildProviderUsageCommand(providerID)
            if (seenCommands[baseCommand]) {
                continue
            }
            seenCommands[baseCommand] = true
            var command = commandWithRunNonce(baseCommand)
            commands[command] = providerID
            commandList.push(command)
            fallbackProviderOrder.push(providerID)
            pendingProviderCount++
        }

        pendingProviderCommands = commands
        fallbackProviderQueue = commandList
        pumpProviderFallbackCommands()
        if (pendingProviderCount === 0) {
            providers = []
            errorText = i18n("No enabled CodexBar providers.")
            loading = false
        }
    }

    function pumpProviderFallbackCommands() {
        var queue = fallbackProviderQueue.slice()
        while (activeProviderFallbackCount < maximumConcurrentProviderFallbackCommands
                && queue.length > 0) {
            var sourceName = queue.shift()
            var providerID = pendingProviderCommands[sourceName] || ""
            if (providerID.length === 0) {
                continue
            }
            activeProviderFallbackCount++
            connectUsageCommand(
                sourceName,
                buildUsageCommandDescriptor("providerFallback", providerID))
        }
        fallbackProviderQueue = queue
    }

    function parseProviderFallbackOutput(sourceName, stdoutText, stderrText) {
        var providerID = pendingProviderCommands[sourceName] || ""
        if (providerID.length === 0) {
            return
        }
        var commands = copyObject(pendingProviderCommands)
        delete commands[sourceName]
        pendingProviderCommands = commands
        finishUsageCommandSource(sourceName)

        if (hasOwnKey(fallbackProviderSeen, providerID)) {
            completeProviderFallbackCommand()
            return
        }
        var seen = copyObject(fallbackProviderSeen)
        seen[providerID] = true
        fallbackProviderSeen = seen

        var normalizedItems = []
        var trimmed = stdoutText.trim()
        if (trimmed.length === 0) {
            normalizedItems.push(normalizeProvider(providerErrorPayload(
                providerID,
                stderrText.trim().length > 0 ? boundedCliMessage(stderrText) : i18n("codexbar did not return JSON."))))
        } else {
            var payload
            try {
                payload = JSON.parse(trimmed)
                var items = Array.isArray(payload) ? payload : [payload]
                var itemLimit = Math.min(items.length, maximumAccountSnapshots)
                for (var i = 0; i < itemLimit; i++) {
                    if (!isCliRecord(items[i])) {
                        continue
                    }
                    var providerItem = copyObject(items[i])
                    // A provider-scoped command may only update the requested
                    // provider, even if a malformed CLI payload claims another id.
                    providerItem.provider = providerID
                    normalizedItems.push(normalizeProvider(providerItem))
                }
            } catch (error) {
                normalizedItems.push(normalizeProvider(providerErrorPayload(
                    providerID,
                    i18n("Could not parse codexbar JSON: %1", error.message))))
            }
        }

        var results = copyObject(fallbackProviderResults)
        results[providerID] = normalizedItems
        fallbackProviderResults = results
        completeProviderFallbackCommand()
    }

    function completeProviderFallbackCommand() {
        activeProviderFallbackCount = Math.max(0, activeProviderFallbackCount - 1)
        pendingProviderCount = Math.max(0, pendingProviderCount - 1)
        pumpProviderFallbackCommands()

        if (pendingProviderCount === 0) {
            finishProviderFallback()
        }
    }

    function finishProviderFallback() {
        var nextProviders = []
        // This global delegate budget deliberately wins over completeness if a
        // future provider-scoped CLI response starts returning many accounts.
        for (var i = 0; i < fallbackProviderOrder.length
                && nextProviders.length < maximumProviderSnapshots; i++) {
            var providerID = fallbackProviderOrder[i]
            var items = fallbackProviderResults[providerID] || []
            for (var j = 0; j < items.length
                    && nextProviders.length < maximumProviderSnapshots; j++) {
                nextProviders.push(items[j])
            }
        }

        markNotificationProvidersFresh(nextProviders)
        providers = nextProviders
        errorText = nextProviders.length === 0 ? i18n("codexbar did not return JSON.") : ""
        lastUpdatedText = i18n("Updated %1", Qt.formatDateTime(new Date(), "hh:mm"))
        loading = false
        pendingProviderCommands = ({})
        fallbackProviderQueue = []
        activeProviderFallbackCount = 0
        fallbackProviderSeen = ({})
        pendingProviderCount = 0
        applyTokenCosts()
    }

    function providerErrorPayload(providerID, message) {
        return {
            provider: providerID,
            source: source.length > 0 ? source : "auto",
            error: {
                code: 1,
                kind: "provider",
                message: message
            }
        }
    }

    function loadAccounts(providerID) {
        var normalizedProviderID = providerKey(providerID)
        if (accountLoadingForProvider(normalizedProviderID)) {
            return
        }

        var command = buildProviderAccountsCommand(normalizedProviderID)
        if (command.length === 0) {
            setAccountError(normalizedProviderID, i18n("Set the codexbar command path in widget settings."))
            return
        }

        setAccountError(normalizedProviderID, "")
        setAccountLoading(normalizedProviderID, true)
        var connectedCommand = commandWithRunNonce(command)
        var commands = copyObject(pendingAccountCommands)
        commands[connectedCommand] = {
            providerID: normalizedProviderID,
            commandSignature: command,
            deadlineMs: Date.now() + accountCommandTimeoutMs
        }
        pendingAccountCommands = commands
        connectUsageCommand(connectedCommand, {
            kind: "account",
            providerID: normalizedProviderID,
            deadlineMs: 0
        })
    }

    function accountCommandIsCurrent(descriptor) {
        return descriptor
            && descriptor.commandSignature === buildProviderAccountsCommand(descriptor.providerID)
    }

    function retireStaleAccountCommands() {
        var commands = copyObject(pendingAccountCommands)
        var staleProviders = ({})
        for (var sourceName in commands) {
            if (!hasOwnKey(commands, sourceName)) {
                continue
            }
            var descriptor = commands[sourceName]
            if (accountCommandIsCurrent(descriptor)) {
                continue
            }
            var providerID = descriptor ? providerMapKey(descriptor.providerID) : ""
            finishUsageCommandSource(sourceName)
            delete commands[sourceName]
            if (providerID.length > 0) {
                staleProviders[providerID] = true
            }
        }
        pendingAccountCommands = commands
        for (var staleProviderID in staleProviders) {
            if (hasOwnKey(staleProviders, staleProviderID)) {
                setAccountLoading(staleProviderID, false)
            }
        }
    }

    function hasPendingUsageCommandTimeouts() {
        for (var sourceName in activeUsageCommands) {
            if (!hasOwnKey(activeUsageCommands, sourceName)) {
                continue
            }
            var descriptor = activeUsageCommands[sourceName]
            if (descriptor && Number(descriptor.deadlineMs) > 0) {
                return true
            }
        }
        return false
    }

    function expireUsageCommands(nowMs) {
        var commands = copyObject(activeUsageCommands)
        var expired = []
        for (var sourceName in commands) {
            if (!hasOwnKey(commands, sourceName)) {
                continue
            }
            var descriptor = commands[sourceName]
            var deadline = descriptor ? Number(descriptor.deadlineMs) : 0
            if (!isFinite(deadline) || deadline <= 0 || nowMs < deadline) {
                continue
            }
            expired.push({ sourceName: sourceName, descriptor: descriptor })
        }
        for (var i = 0; i < expired.length; i++) {
            handleUsageCommandTimeout(expired[i].sourceName, expired[i].descriptor)
        }
    }

    function handleUsageCommandTimeout(sourceName, descriptor) {
        if (!descriptor || !activeUsageCommands[sourceName]) {
            return
        }

        if (descriptor.kind === "usage" && sourceName === connectedCommandSource) {
            connectedCommandSource = ""
            finishUsageCommandSource(sourceName)
            if (canUseProviderFallback()) {
                startProviderFallback()
                return
            }
            providers = []
            loading = false
            errorText = i18n("Loading usage timed out. Try again.")
            return
        }

        if (descriptor.kind === "cost" && sourceName === connectedCostCommandSource) {
            connectedCostCommandSource = ""
            finishUsageCommandSource(sourceName)
            costErrorText = i18n("Loading cost data timed out. Try again.")
            applyTokenCosts()
            return
        }

        if (descriptor.kind === "providerConfig"
                && sourceName === connectedProviderConfigCommandSource) {
            connectedProviderConfigCommandSource = ""
            finishUsageCommandSource(sourceName)
            providers = []
            loading = false
            errorText = i18n("Loading provider configuration timed out. Try again.")
            return
        }

        if (descriptor.kind === "providerFallback" && pendingProviderCommands[sourceName]) {
            parseProviderFallbackOutput(
                sourceName,
                "",
                i18n("Loading usage timed out. Try again."))
            return
        }

        finishUsageCommandSource(sourceName)
    }

    function hasPendingAccountCommands() {
        for (var sourceName in pendingAccountCommands) {
            if (hasOwnKey(pendingAccountCommands, sourceName)) {
                return true
            }
        }
        return false
    }

    function expirePendingAccountCommands(nowMs) {
        var commands = copyObject(pendingAccountCommands)
        var expired = []
        for (var pendingSourceName in commands) {
            if (!hasOwnKey(commands, pendingSourceName)) {
                continue
            }
            var descriptor = commands[pendingSourceName]
            var deadline = Number(descriptor.deadlineMs)
            if (!isFinite(deadline) || nowMs < deadline) {
                continue
            }
            expired.push({ sourceName: pendingSourceName, providerID: descriptor.providerID })
            delete commands[pendingSourceName]
        }
        if (expired.length === 0) {
            return
        }

        pendingAccountCommands = commands
        for (var i = 0; i < expired.length; i++) {
            var item = expired[i]
            var sourceName = item.sourceName
            var providerID = item.providerID
            finishUsageCommandSource(sourceName)
            setAccountLoading(providerID, false)
            setAccountError(providerID, i18n("Loading accounts timed out. Try again."))
        }
    }

    function parseProviderAccountsOutput(sourceName, stdoutText, stderrText) {
        var descriptor = pendingAccountCommands[sourceName] || null
        var providerID = descriptor ? descriptor.providerID : ""
        if (providerID.length === 0) {
            return
        }

        var commands = copyObject(pendingAccountCommands)
        delete commands[sourceName]
        pendingAccountCommands = commands
        finishUsageCommandSource(sourceName)
        setAccountLoading(providerID, false)
        if (!accountCommandIsCurrent(descriptor)) {
            return
        }

        var trimmed = stdoutText.trim()
        if (trimmed.length === 0) {
            setAccountError(providerID, stderrText.trim().length > 0 ? boundedCliMessage(stderrText) : i18n("codexbar did not return account data."))
            return
        }

        var payload
        try {
            payload = JSON.parse(trimmed)
        } catch (error) {
            setAccountError(providerID, i18n("Could not parse codexbar account JSON: %1", error.message))
            return
        }

        var items = Array.isArray(payload) ? payload : [payload]
        var options = []
        var message = ""
        var sawMissingTokenAccountsError = false
        var itemLimit = Math.min(items.length, maximumAccountSnapshots)
        for (var i = 0; i < itemLimit; i++) {
            var item = items[i]
            if (!isCliRecord(item)) {
                continue
            }
            var accountItem = copyObject(item)
            accountItem.provider = providerID
            var normalized = normalizeProvider(accountItem)
            if (normalized.error.length > 0 && accountLabel(normalized).length === 0) {
                if (isMissingTokenAccountsError(normalized.error)) {
                    sawMissingTokenAccountsError = true
                } else {
                    message = normalized.error
                }
                continue
            }
            options.push(normalized)
        }

        var dedupedOptions = dedupeAccountOptions(options)
        var accountError = ""
        if (dedupedOptions.length === 0) {
            if (message.length > 0) {
                accountError = message
            } else if (items.length > 0 && !sawMissingTokenAccountsError) {
                accountError = i18n("codexbar did not return account data.")
            }
        }
        if (accountError.length === 0) {
            setAccountOptions(providerID, dedupedOptions)
        }
        setAccountError(providerID, accountError)
    }

    function isMissingTokenAccountsError(errorMessage) {
        // codexbar --all-accounts reports "No token accounts configured for
        // <provider>." even when the provider works through OAuth/CLI auth
        // without named token accounts; that is an empty list, not a failure.
        return String(errorMessage || "").toLowerCase().indexOf("no token accounts configured") !== -1
    }

    function dedupeAccountOptions(items) {
        var seen = ({})
        var result = []
        for (var i = 0; i < items.length; i++) {
            var label = accountLabel(items[i])
            var key = "account:" + label
            if (label.length === 0 || hasOwnKey(seen, key)) {
                continue
            }
            seen[key] = true
            result.push(items[i])
        }
        return result
    }

    function parseCostOutput(stdoutText, stderrText) {
        var trimmed = stdoutText.trim()
        if (trimmed.length === 0) {
            costErrorText = stderrText.trim().length > 0 ? boundedCliMessage(stderrText) : i18n("codexbar cost did not return JSON.")
            applyTokenCosts()
            return
        }

        var payload
        try {
            payload = JSON.parse(trimmed)
        } catch (error) {
            costErrorText = i18n("Could not parse codexbar cost JSON: %1", error.message)
            applyTokenCosts()
            return
        }

        var items = Array.isArray(payload) ? payload : [payload]
        var nextCosts = ({})
        var costMessage = ""
        var itemLimit = Math.min(items.length, maximumCostSnapshots)
        for (var i = 0; i < itemLimit; i++) {
            var item = items[i]
            if (!isCliRecord(item)) {
                continue
            }
            if (costMessage.length === 0 && item && item.error && item.error.message) {
                costMessage = boundedCliMessage(item.error.message)
            }
            var cost = normalizeTokenCost(item)
            var providerID = cost ? providerMapKey(cost.provider) : ""
            if (cost && providerID.length > 0) {
                nextCosts[providerID] = cost
            }
        }

        if (costMessage.length > 0) {
            var mergedCosts = copyObject(tokenCosts)
            for (var providerKeyName in nextCosts) {
                if (hasOwnKey(nextCosts, providerKeyName)) {
                    mergedCosts[providerKeyName] = nextCosts[providerKeyName]
                }
            }
            tokenCosts = mergedCosts
            costErrorText = costMessage
        } else {
            tokenCosts = nextCosts
            costErrorText = ""
        }
        applyTokenCosts()
    }

    function normalizeTokenCost(item) {
        if (!item || !item.provider) {
            return null
        }

        var providerID = providerMapKey(item.provider)
        if (providerID.length === 0) {
            return null
        }
        var currency = boundedDisplayText(item.currencyCode || "USD", 12)
        var windowLabel = boundedDisplayText(item.historyLabel || costHistoryWindowLabel(item), 120)
        return {
            provider: providerID,
            title: i18n("Cost"),
            sessionLine: costLine(i18n("Today"), item.sessionCostUSD, item.sessionTokens, currency),
            monthLine: costLine(windowLabel, item.last30DaysCostUSD, item.last30DaysTokens, currency),
            hintLine: tokenCostHint(providerID),
            totals: normalizeCostTotals(item.totals, item.last30DaysCostUSD, item.last30DaysTokens, currency),
            models: normalizeCostModels(item.daily, currency, costHistoryDays),
            daily: normalizeCostDaily(item.daily, currency, costHistoryDays)
        }
    }

    function costHistoryWindowLabel(item) {
        var rawDays = item && item.historyDays !== undefined && item.historyDays !== null
            ? Number(item.historyDays)
            : Number(costHistoryDays)
        if (!isFinite(rawDays) || rawDays <= 0) {
            return i18n("Last 30 days")
        }
        var days = Math.max(1, Math.floor(rawDays))
        return days === 1 ? i18n("Today") : i18np("Last %1 day", "Last %1 days", days)
    }

    function normalizeCostDaily(items, currency, days) {
        var result = []
        if (!items || !Array.isArray(items)) {
            return result
        }

        var historyDays = isFinite(Number(days)) ? Math.max(1, Math.min(maximumCostHistoryPoints, Number(days))) : 30
        var inspectedItems = 0
        for (var i = items.length - 1; i >= 0
                && result.length < historyDays
                && inspectedItems < maximumCostHistoryScanItems; i--) {
            inspectedItems++
            var item = isCliRecord(items[i]) ? items[i] : null
            if (!item) {
                continue
            }
            var cost = Number(item.totalCost !== undefined ? item.totalCost : item.costUSD)
            var tokens = Number(item.totalTokens !== undefined ? item.totalTokens : item.tokens)
            var inputTokens = Number(item.inputTokens)
            var outputTokens = Number(item.outputTokens)
            var cacheReadTokens = Number(item.cacheReadTokens)
            var cacheCreationTokens = Number(item.cacheCreationTokens !== undefined ? item.cacheCreationTokens : item.cacheWriteTokens)
            if (!isFinite(tokens)) {
                tokens = sumTokenParts(inputTokens, outputTokens, cacheReadTokens, cacheCreationTokens)
            }
            if (!isFinite(cost) && !isFinite(tokens) && !isFinite(inputTokens) && !isFinite(outputTokens)) {
                continue
            }
            result.unshift({
                label: boundedDisplayText(item.date || item.day || item.dayKey || "", 120),
                cost: isFinite(cost) ? Math.max(0, cost) : 0,
                tokens: isFinite(tokens) ? Math.max(0, tokens) : 0,
                inputTokens: isFinite(inputTokens) ? Math.max(0, inputTokens) : 0,
                outputTokens: isFinite(outputTokens) ? Math.max(0, outputTokens) : 0,
                cacheReadTokens: isFinite(cacheReadTokens) ? Math.max(0, cacheReadTokens) : 0,
                cacheCreationTokens: isFinite(cacheCreationTokens) ? Math.max(0, cacheCreationTokens) : 0,
                currency: boundedDisplayText(currency || "USD", 12)
            })
        }
        return result
    }

    function normalizeCostTotals(totals, fallbackCost, fallbackTokens, currency) {
        var source = totals || ({})
        var cost = Number(source.totalCost !== undefined ? source.totalCost : fallbackCost)
        var tokens = Number(source.totalTokens !== undefined ? source.totalTokens : fallbackTokens)
        var inputTokens = Number(source.inputTokens)
        var outputTokens = Number(source.outputTokens)
        var cacheReadTokens = Number(source.cacheReadTokens)
        var cacheCreationTokens = Number(source.cacheCreationTokens !== undefined ? source.cacheCreationTokens : source.cacheWriteTokens)
        if (!isFinite(tokens)) {
            tokens = sumTokenParts(inputTokens, outputTokens, cacheReadTokens, cacheCreationTokens)
        }
        return {
            cost: isFinite(cost) ? Math.max(0, cost) : 0,
            tokens: isFinite(tokens) ? Math.max(0, tokens) : 0,
            inputTokens: isFinite(inputTokens) ? Math.max(0, inputTokens) : 0,
            outputTokens: isFinite(outputTokens) ? Math.max(0, outputTokens) : 0,
            cacheReadTokens: isFinite(cacheReadTokens) ? Math.max(0, cacheReadTokens) : 0,
            cacheCreationTokens: isFinite(cacheCreationTokens) ? Math.max(0, cacheCreationTokens) : 0,
            currency: boundedDisplayText(currency || "USD", 12)
        }
    }

    function sumTokenParts(inputTokens, outputTokens, cacheReadTokens, cacheCreationTokens) {
        var total = 0
        var values = [inputTokens, outputTokens, cacheReadTokens, cacheCreationTokens]
        for (var i = 0; i < values.length; i++) {
            if (isFinite(Number(values[i])) && Number(values[i]) > 0) {
                total += Number(values[i])
            }
        }
        return total > 0 ? total : Number.NaN
    }

    function normalizeCostModels(items, currency, days) {
        var byName = ({})
        if (!items || !Array.isArray(items)) {
            return []
        }

        var historyDays = isFinite(Number(days)) ? Math.max(1, Math.min(maximumCostHistoryPoints, Number(days))) : 30
        var firstItem = Math.max(0, items.length - historyDays)
        for (var i = firstItem; i < items.length; i++) {
            var breakdowns = items[i] && Array.isArray(items[i].modelBreakdowns)
                ? items[i].modelBreakdowns
                : []
            var breakdownLimit = Math.min(breakdowns.length, maximumModelBreakdownsPerDay)
            for (var j = 0; j < breakdownLimit; j++) {
                var breakdown = breakdowns[j] || ({})
                var name = boundedDisplayText(breakdown.modelName || breakdown.model || "", 120)
                if (name.length === 0 || isUnsafeObjectKey(name)) {
                    continue
                }
                var cost = Number(breakdown.cost !== undefined ? breakdown.cost : breakdown.totalCost)
                var tokens = Number(breakdown.totalTokens !== undefined ? breakdown.totalTokens : breakdown.tokens)
                if (!isFinite(cost) && !isFinite(tokens)) {
                    continue
                }
                if (!hasOwnKey(byName, name)) {
                    byName[name] = {
                        label: name,
                        cost: 0,
                        tokens: 0,
                        currency: boundedDisplayText(currency || "USD", 12)
                    }
                }
                if (isFinite(cost)) {
                    byName[name].cost += Math.max(0, cost)
                }
                if (isFinite(tokens)) {
                    byName[name].tokens += Math.max(0, tokens)
                }
            }
        }

        var rows = []
        for (var modelName in byName) {
            if (!hasOwnKey(byName, modelName)) {
                continue
            }
            rows.push(byName[modelName])
        }
        rows.sort(function(a, b) {
            if (b.cost !== a.cost) {
                return b.cost - a.cost
            }
            return b.tokens - a.tokens
        })
        return rows.slice(0, 6)
    }

    function costSparklineMax(points) {
        var maxCost = 0
        if (!points) {
            return maxCost
        }
        for (var i = 0; i < points.length; i++) {
            maxCost = Math.max(maxCost, Number(points[i].cost) || 0)
        }
        return maxCost
    }

    function paintRoundedTopBar(context, x, baseline, width, height, radius) {
        var safeWidth = Math.max(0, width)
        var safeHeight = Math.max(0, height)
        var top = baseline - safeHeight
        var corner = Math.max(0, Math.min(radius, safeWidth / 2, safeHeight))

        context.beginPath()
        context.moveTo(x, baseline)
        context.lineTo(x, top + corner)
        context.quadraticCurveTo(x, top, x + corner, top)
        context.lineTo(x + safeWidth - corner, top)
        context.quadraticCurveTo(x + safeWidth, top, x + safeWidth, top + corner)
        context.lineTo(x + safeWidth, baseline)
        context.closePath()
        context.fill()
    }

    // Bar charts must fit the canvas for every point count the normalizers
    // allow (up to 365 cost-history days and 120 detail-chart points). Deriving
    // both the gap and the bar width from one step keeps the last bar inside
    // `width`; a fixed minimum gap or bar width would push dense charts off the
    // right edge and silently hide the newest data.
    function chartBarGeometry(width, count) {
        var points = Math.max(1, Math.floor(Number(count) || 0))
        var step = Math.max(0, Number(width) || 0) / points
        var gap = Math.max(0, Math.min(4, step / 4))
        return {
            step: step,
            gap: gap,
            barWidth: Math.max(1, step - gap)
        }
    }

    function buildChartBarGradient(context, accent, baseline, topOpacity, bottomOpacity) {
        var gradient = context.createLinearGradient(0, 0, 0, Math.max(1, baseline))
        gradient.addColorStop(0, canvasColor(accent, topOpacity))
        gradient.addColorStop(1, canvasColor(accent, bottomOpacity))
        return gradient
    }

    function costSparklineSummary(points) {
        if (!points || points.length === 0) {
            return ""
        }
        var last = points[points.length - 1]
        var label = last.label && last.label.length > 0 ? last.label : i18n("Latest")
        return i18n("%1: %2", label, amountString(last.cost, last.currency || "USD"))
    }

    function costBreakdownRows(tokenCost) {
        if (!tokenCost || !tokenCost.totals) {
            return []
        }

        var totals = tokenCost.totals
        var rows = []
        appendTokenBreakdownRow(rows, i18n("Total tokens"), totals.tokens)
        appendTokenBreakdownRow(rows, i18n("Input"), totals.inputTokens)
        appendTokenBreakdownRow(rows, i18n("Output"), totals.outputTokens)
        appendTokenBreakdownRow(rows, i18n("Cache read"), totals.cacheReadTokens)
        appendTokenBreakdownRow(rows, i18n("Cache write"), totals.cacheCreationTokens)
        return rows
    }

    function appendTokenBreakdownRow(rows, label, tokens) {
        var value = Number(tokens)
        if (!isFinite(value) || value <= 0) {
            return
        }
        rows.push({
            label: label,
            value: tokenCountString(value)
        })
    }

    function costModelRows(tokenCost) {
        if (!tokenCost || !tokenCost.models) {
            return []
        }

        var rows = []
        for (var i = 0; i < tokenCost.models.length; i++) {
            var item = tokenCost.models[i]
            rows.push({
                label: item.label,
                value: costTokenSummary(item.cost, item.tokens, item.currency)
            })
        }
        return rows
    }

    function costHistoryRows(tokenCost) {
        if (!tokenCost || !tokenCost.daily || tokenCost.daily.length === 0) {
            return []
        }

        var first = Math.max(0, tokenCost.daily.length - 7)
        var visibleDaily = tokenCost.daily.slice(first)
        var rows = []
        var maxCost = costSparklineMax(visibleDaily)
        for (var i = visibleDaily.length - 1; i >= 0; i--) {
            var item = visibleDaily[i]
            var cost = Math.max(0, Number(item.cost) || 0)
            var value = compactCostTokenSummary(cost, item.tokens, item.currency)
            rows.push({
                label: item.label && item.label.length > 0 ? item.label : i18n("Latest"),
                value: value.length > 0 ? value : amountString(0, item.currency || "USD"),
                percent: maxCost > 0 ? Math.max(3, cost * 100 / maxCost) : 0,
                isPeak: maxCost > 0 && cost === maxCost
            })
        }
        return rows
    }

    function costPeakLine(points) {
        if (!points || points.length === 0) {
            return ""
        }

        var peak = null
        for (var i = 0; i < points.length; i++) {
            var cost = Number(points[i].cost) || 0
            if (!peak || cost > peak.cost) {
                peak = {
                    label: points[i].label && points[i].label.length > 0 ? points[i].label : i18n("Latest"),
                    cost: cost,
                    currency: points[i].currency || "USD"
                }
            }
        }
        if (!peak || peak.cost <= 0) {
            return ""
        }
        return i18n("Peak: %1 - %2", peak.label, amountString(peak.cost, peak.currency))
    }

    function costAverageDailyLine(points) {
        if (!points || points.length === 0) {
            return ""
        }

        var total = 0
        var currency = "USD"
        for (var i = 0; i < points.length; i++) {
            total += Math.max(0, Number(points[i].cost) || 0)
            if (points[i].currency) {
                currency = points[i].currency
            }
        }
        if (total <= 0) {
            return ""
        }
        return i18n("Average/day: %1", amountString(total / points.length, currency))
    }

    function costPerMillionLine(tokenCost) {
        if (!tokenCost || !tokenCost.totals) {
            return ""
        }
        var cost = Number(tokenCost.totals.cost)
        var tokens = Number(tokenCost.totals.tokens)
        if (!isFinite(cost) || !isFinite(tokens) || cost <= 0 || tokens <= 0) {
            return ""
        }
        return i18n("Average: %1 / 1M tokens", amountString(cost * 1000000 / tokens, tokenCost.totals.currency || "USD"))
    }

    function costTokenSummary(cost, tokens, currency) {
        var parts = []
        if (isFinite(Number(cost)) && Number(cost) > 0) {
            parts.push(amountString(Number(cost), currency || "USD"))
        }
        if (isFinite(Number(tokens)) && Number(tokens) > 0) {
            parts.push(i18n("%1 tokens", tokenCountString(Number(tokens))))
        }
        return parts.join(" · ")
    }

    readonly property int usageDashboardRowLimit: 10
    readonly property int usageDashboardMaxDepth: 4

    function usageDashboard(providerID, usage, item) {
        var kpis = []
        var rows = []
        var dashboardState = {
            rowLimit: usageDashboardRowLimit,
            maxDepth: usageDashboardMaxDepth,
            seen: []
        }
        var sources = [
            { title: i18n("Codex dashboard"), value: item.openaiDashboard },
            { title: i18n("OpenAI API"), value: usage.openAIAPIUsage },
            { title: i18n("OpenRouter"), value: usage.openRouterUsage },
            { title: i18n("Claude Admin"), value: usage.claudeAdminAPIUsage },
            { title: i18n("Poe"), value: usage.poeUsage },
            { title: i18n("DeepSeek"), value: usage.deepseekUsage },
            { title: i18n("MiniMax"), value: usage.minimaxUsage },
            { title: i18n("Z.ai"), value: usage.zaiUsage }
        ]

        for (var i = 0; i < sources.length; i++) {
            appendDashboardSource(kpis, rows, sources[i].title, sources[i].value, dashboardState, 0)
            if (rows.length >= dashboardState.rowLimit && kpis.length >= 4) {
                break
            }
        }

        if (kpis.length === 0 && rows.length === 0) {
            return null
        }
        return {
            kpis: kpis.slice(0, 4),
            rows: rows.slice(0, usageDashboardRowLimit)
        }
    }

    function appendDashboardSource(kpis, rows, title, source, state, depth) {
        if (!isDashboardObject(source)) {
            return
        }
        if (!state) {
            state = {
                rowLimit: usageDashboardRowLimit,
                maxDepth: usageDashboardMaxDepth,
                seen: []
            }
        }
        depth = depth || 0

        var sourceRows = usageDashboardRows(source, state, depth)
        if (sourceRows.length === 0) {
            return
        }

        if (kpis.length < 4) {
            kpis.push({
                label: title,
                value: sourceRows[0].value
            })
        }

        for (var i = 0; i < sourceRows.length && rows.length < state.rowLimit; i++) {
            rows.push({
                label: sourceRows[i].label,
                value: sourceRows[i].value
            })
        }
    }

    function isDashboardObject(source) {
        return source && typeof source === "object" && !Array.isArray(source)
    }

    function usageDashboardRows(source, state, depth) {
        state = state || {
            rowLimit: usageDashboardRowLimit,
            maxDepth: usageDashboardMaxDepth,
            seen: []
        }
        depth = depth || 0
        var rows = []
        if (!isDashboardObject(source) || depth > state.maxDepth || state.seen.indexOf(source) !== -1) {
            return rows
        }
        state.seen.push(source)
        appendDashboardMetric(rows, i18n("Code review remaining"), source.codeReviewRemainingPercent, "percent")
        appendDashboardMetric(rows, i18n("Credits remaining"), source.creditsRemaining, "number")
        appendDashboardMetric(rows, i18n("Plan"), source.accountPlan, "text")
        appendDashboardMetric(rows, i18n("Signed in"), source.signedInEmail, "text")

        appendDashboardPeriodRow(rows, i18n("Today"), source.currentDay || source.today)
        appendDashboardPeriodRow(rows, i18n("7d"), source.last7Days)
        appendDashboardPeriodRow(rows, source.historyWindowLabel || i18n("30d"), source.last30Days)
        appendDashboardPeriodRow(rows, i18n("This month"), source.currentMonth || source.month || source.billingSummary)

        appendDashboardTopRow(rows, i18n("Top model"), source.topModels)
        appendDashboardTopRow(rows, i18n("Usage mix"), source.topUsageTypes)
        appendDashboardLatestDailyRow(rows, source.daily)
        appendDashboardLatestBreakdownRow(rows, source.usageBreakdown || source.dailyBreakdown)
        if (rows.length < state.rowLimit && depth < state.maxDepth && isDashboardObject(source.modelUsage)) {
            appendDashboardSource([], rows, i18n("Models"), source.modelUsage, state, depth + 1)
        }
        state.seen.pop()
        return rows.slice(0, state.rowLimit)
    }

    function appendDashboardMetric(rows, label, value, kind) {
        var text = dashboardValueText(value, kind)
        if (text.length === 0) {
            return
        }
        rows.push({
            label: boundedDisplayText(label, 120),
            value: text
        })
    }

    function appendDashboardPeriodRow(rows, label, source) {
        if (!isCliRecord(source)) {
            return
        }
        var parts = []
        var currency = boundedDisplayText(source.currency || source.currencyCode || "USD", 12)
        var cost = source.costUSD !== undefined ? source.costUSD : (source.cost !== undefined ? source.cost : source.totalCost)
        var tokens = source.totalTokens !== undefined ? source.totalTokens : source.tokens
        var requests = source.requests !== undefined ? source.requests : source.requestCount
        var points = source.points !== undefined ? source.points : source.totalPoints

        if (isFinite(Number(cost))) {
            parts.push(amountString(Number(cost), currency))
        }
        if (isFinite(Number(tokens)) && Number(tokens) > 0) {
            parts.push(i18n("%1 tokens", tokenCountString(Number(tokens))))
        }
        if (isFinite(Number(requests)) && Number(requests) > 0) {
            parts.push(i18n("%1 requests", tokenCountString(Number(requests))))
        }
        if (isFinite(Number(points)) && Number(points) > 0) {
            parts.push(i18n("%1 points", tokenCountString(Number(points))))
        }
        if (parts.length === 0) {
            appendDashboardMetric(rows, label, source.value || source.total || source.used, "number")
            return
        }
        rows.push({
            label: boundedDisplayText(label, 120),
            value: boundedDisplayText(parts.join(" · "), 500)
        })
    }

    function appendDashboardTopRow(rows, label, items) {
        if (!items || !Array.isArray(items) || items.length === 0) {
            return
        }
        var item = items[0] || ({})
        var name = boundedDisplayText(item.name || item.model || item.label || item.type || "", 120)
        if (name.length === 0) {
            return
        }
        var suffix = dashboardTopSuffix(item)
        rows.push({
            label: boundedDisplayText(label, 120),
            value: boundedDisplayText(suffix.length > 0 ? i18n("%1 (%2)", name, suffix) : name, 500)
        })
    }

    function appendDashboardLatestDailyRow(rows, items) {
        if (!items || !Array.isArray(items) || items.length === 0) {
            return
        }
        var item = items[items.length - 1] || ({})
        appendDashboardPeriodRow(rows, item.label || item.day || item.date || i18n("Latest"), item)
    }

    function appendDashboardLatestBreakdownRow(rows, items) {
        if (!items || !Array.isArray(items) || items.length === 0) {
            return
        }
        var item = items[items.length - 1] || ({})
        var label = item.day || item.date || item.label || i18n("Latest dashboard day")
        appendDashboardPeriodRow(rows, label, {
            costUSD: item.costUSD,
            totalTokens: item.totalTokens,
            requests: item.requests,
            points: item.points,
            value: item.totalCreditsUsed
        })
    }

    function dashboardValueText(value, kind) {
        if (value === null || value === undefined) {
            return ""
        }
        if (kind === "text") {
            return boundedDisplayText(value, 120)
        }
        if (kind === "percent") {
            var percent = Number(value)
            return isFinite(percent) ? i18n("%1%", Math.round(percent)) : ""
        }
        if (kind === "tokens") {
            var tokens = Number(value)
            return isFinite(tokens) ? i18n("%1 tokens", tokenCountString(tokens)) : ""
        }
        if (kind === "currency") {
            var cost = Number(value)
            return isFinite(cost) ? amountString(cost, "USD") : ""
        }
        var number = Number(value)
        if (!isFinite(number)) {
            return boundedDisplayText(value, 120)
        }
        return tokenCountString(number)
    }

    function dashboardTopSuffix(item) {
        if (isFinite(Number(item.costUSD))) {
            return amountString(Number(item.costUSD), "USD")
        }
        if (isFinite(Number(item.points))) {
            return i18n("%1 points", tokenCountString(Number(item.points)))
        }
        if (isFinite(Number(item.totalTokens))) {
            return i18n("%1 tokens", tokenCountString(Number(item.totalTokens)))
        }
        if (isFinite(Number(item.requests))) {
            return i18n("%1 requests", tokenCountString(Number(item.requests)))
        }
        return ""
    }

    function compactCostTokenSummary(cost, tokens, currency) {
        var parts = []
        if (isFinite(Number(cost)) && Number(cost) > 0) {
            parts.push(amountString(Number(cost), currency || "USD"))
        }
        if (isFinite(Number(tokens)) && Number(tokens) > 0) {
            parts.push(tokenCountString(Number(tokens)))
        }
        return parts.join(" · ")
    }

    function providerTokenCost(providerID) {
        var key = providerMapKey(providerID)
        return key.length > 0 ? tokenCosts[key] || null : null
    }

    function applyTokenCosts() {
        if (!providers || providers.length === 0) {
            return
        }

        var nextProviders = []
        for (var i = 0; i < providers.length; i++) {
            var item = copyObject(providers[i])
            item.tokenCost = providerTokenCost(item.provider)
            nextProviders.push(item)
        }
        providers = nextProviders
    }

    function selectedAccountForProvider(providerID) {
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return ""
        }
        var selected = selectedAccounts[key]
        return selected ? String(selected) : ""
    }

    function accountOptionsForProvider(providerID) {
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return []
        }
        return accountOptions[key] || []
    }

    function accountErrorForProvider(providerID) {
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return ""
        }
        return accountErrors[key] ? String(accountErrors[key]) : ""
    }

    function accountLoadingForProvider(providerID) {
        var key = providerMapKey(providerID)
        return key.length > 0 && accountLoading[key] === true
    }

    function setAccountOptions(providerID, options) {
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return
        }
        var next = copyObject(accountOptions)
        next[key] = options || []
        accountOptions = next
    }

    function setAccountError(providerID, message) {
        var next = copyObject(accountErrors)
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return
        }
        var cleanMessage = boundedCliMessage(message)
        if (cleanMessage.length > 0) {
            next[key] = cleanMessage
        } else {
            delete next[key]
        }
        accountErrors = next
    }

    function setAccountLoading(providerID, value) {
        var next = copyObject(accountLoading)
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return
        }
        if (value) {
            next[key] = true
        } else {
            delete next[key]
        }
        accountLoading = next
    }

    function accountLabel(item) {
        if (!item) {
            return ""
        }
        if (item.account && item.account.length > 0) {
            return item.account
        }
        if (item.organization && item.organization.length > 0) {
            return item.organization
        }
        if (item.loginMethod && item.loginMethod.length > 0) {
            return item.loginMethod
        }
        return ""
    }

    function accountSubtitle(item) {
        if (!item) {
            return ""
        }
        var parts = []
        if (item.loginMethod && item.loginMethod.length > 0) {
            parts.push(item.loginMethod)
        }
        if (item.organization && item.organization.length > 0 && item.organization !== item.account) {
            parts.push(item.organization)
        }
        return parts.join(" · ")
    }

    function accountIsSelected(option, currentItem) {
        if (!option) {
            return false
        }
        var label = accountLabel(option)
        var selected = selectedAccountForProvider(option.provider)
        if (selected.length > 0) {
            return label === selected
        }
        return currentItem && currentItem.provider === option.provider && label === accountLabel(currentItem)
    }

    function selectAccount(providerID, accountLabel) {
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return
        }
        var label = String(accountLabel || "")
        var next = copyObject(selectedAccounts)
        if (label.length > 0) {
            next[key] = label
        } else {
            delete next[key]
        }
        selectedAccounts = next
        setNotificationProviderRefreshPending(key, true)

        var options = accountOptionsForProvider(key)
        for (var i = 0; i < options.length; i++) {
            if (root.accountLabel(options[i]) === label) {
                replaceProviderSnapshot(key, options[i])
                Qt.callLater(refreshNow)
                return
            }
        }
        Qt.callLater(refreshNow)
    }

    function replaceProviderSnapshot(providerID, snapshot) {
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return
        }
        var replacement = copyObject(snapshot)
        replacement.tokenCost = providerTokenCost(key)
        var nextProviders = []
        for (var i = 0; i < providers.length; i++) {
            nextProviders.push(providers[i].provider === key ? replacement : providers[i])
        }
        providers = nextProviders
    }

    function normalizeProvider(item) {
        var usage = isCliRecord(item.usage) ? item.usage : ({})
        var pace = isCliRecord(item.pace) ? item.pace : ({})
        var rows = []
        var providerID = providerMapKey(item.provider || "unknown")
        if (providerID.length === 0) {
            providerID = "unknown"
        }

        var primaryRow = addWindow(rows, rateWindowLabel(providerID, "primary"), usage.primary, pace.primary, true, "primary")
        addWindow(rows, rateWindowLabel(providerID, "secondary"), usage.secondary, pace.secondary, true, "secondary")
        addWindow(rows, rateWindowLabel(providerID, "tertiary"), usage.tertiary, null, true, "tertiary")

        var extras = Array.isArray(usage.extraRateWindows) ? usage.extraRateWindows : []
        var extraLimit = Math.min(extras.length, maximumExtraRateWindows)
        for (var i = 0; i < extraLimit; i++) {
            var extra = extras[i]
            if (isCliRecord(extra) && isCliRecord(extra.window)) {
                addWindow(rows, boundedDisplayText(extra.title || extra.id || i18n("Extra"), 120), extra.window, null, extra.usageKnown !== false, "extra")
            }
        }

        var identity = isCliRecord(usage.identity) ? usage.identity : ({})
        var error = isCliRecord(item.error) ? item.error : null
        var status = isCliRecord(item.status) ? item.status : null
        var severity = statusSeverity(status)
        var credits = isCliRecord(item.credits) ? item.credits : null
        var displayName = item.displayName || item.title || providerDisplayNames[providerID] || ""
        var providerDetails = UsageDetails.normalizeSections(usage.details)
        var providerUsageDashboard = providerDetails.length > 0 ? null : usageDashboard(providerID, usage, item)
        var hasSupplementalUsage = providerDetails.length > 0 || providerUsageDashboard !== null
        var placeholder = providerPlaceholder(providerID, rows, usage, item, error, hasSupplementalUsage)

        return {
            provider: providerID,
            title: boundedDisplayText(providerTitle(providerID, displayName), 120),
            source: boundedDisplayText(item.source || "", 120),
            version: boundedDisplayText(item.version || "", 120),
            account: boundedDisplayText(item.account || identity.accountEmail || usage.accountEmail || "", 256),
            organization: boundedDisplayText(identity.accountOrganization || usage.accountOrganization || "", 256),
            loginMethod: boundedDisplayText(identity.loginMethod || usage.loginMethod || "", 120),
            rows: rows,
            primaryRow: primaryRow,
            providerDetails: providerDetails,
            usageDashboard: providerUsageDashboard,
            providerCost: providerCostSection(providerID, usage.providerCost),
            resetCredits: resetCreditsSection(providerID, usage.codexResetCredits),
            tokenCost: providerTokenCost(providerID),
            planText: boundedDisplayText(planText(providerID, usage, item), 120),
            dashboardUrl: providerDashboardUrl(providerID),
            statusUrl: safeStatusUrl(providerID, status && status.url ? status.url : ""),
            changelogUrl: providerChangelogUrl(providerID),
            credits: credits && credits.remaining !== null && credits.remaining !== undefined && isFinite(Number(credits.remaining))
                ? Number(credits.remaining)
                : null,
            status: boundedDisplayText(status ? statusText(status) : "", 500),
            statusSeverity: severity,
            statusIncidentKey: boundedDisplayText(statusIncidentKey(status), 128),
            hasIncident: severity.length > 0,
            error: boundedCliMessage(error && error.message ? error.message : ""),
            placeholder: placeholder,
            updatedAt: boundedDisplayText(usage.updatedAt || (credits ? credits.updatedAt : ""), 128)
        }
    }

    function providerPlaceholder(providerID, rows, usage, item, error, hasSupplementalUsage) {
        if ((rows && rows.length > 0) || hasSupplementalUsage === true) {
            return ""
        }

        var message = error && error.message ? String(error.message).trim() : ""
        if (message.length > 0 && message !== "Found sessions, but no rate limit events yet.") {
            return ""
        }

        if (rateLimitsUnavailable(providerID, usage, item)) {
            return i18n("Limits not available")
        }

        return i18n("No usage yet")
    }

    function rateLimitsUnavailable(providerID, usage, item) {
        var key = providerKey(providerID)
        if (key !== "antigravity" && key !== "doubao" && key !== "codex") {
            return false
        }

        var identity = usage && usage.identity ? usage.identity : ({})
        var hasIdentity = (item && item.account && item.account.length > 0)
            || (identity.accountEmail && identity.accountEmail.length > 0)
            || (identity.accountOrganization && identity.accountOrganization.length > 0)
            || (identity.loginMethod && identity.loginMethod.length > 0)
        if (!hasIdentity) {
            return false
        }

        return !usage.primary && !usage.secondary && !usage.tertiary
    }

    function addWindow(rows, label, window, pace, usageKnown, lane) {
        if (!isCliRecord(window)) {
            return null
        }

        var known = usageKnown !== false
        var used = Number(window.usedPercent)
        var hasPercent = known && isFinite(used)
        var paceValue = pace
            && pace.expectedUsedPercent !== null
            && pace.expectedUsedPercent !== undefined
            && isFinite(Number(pace.expectedUsedPercent))
            ? clamp(Number(pace.expectedUsedPercent), 0, 100)
            : -1
        var row = {
            lane: lane || "",
            label: boundedDisplayText(label, 120),
            hasPercent: hasPercent,
            usedPercent: hasPercent ? clamp(used, 0, 100) : 0,
            leftPercent: hasPercent ? clamp(100 - used, 0, 100) : 0,
            pacePercent: paceValue,
            paceOnTop: !pace || pace.willLastToReset !== false,
            resetsAt: boundedDisplayText(
                window.resetsAt === undefined || window.resetsAt === null ? "" : window.resetsAt,
                128),
            resetDescription: boundedDisplayText(window.resetDescription || "", 500),
            reset: boundedDisplayText(resetText(window, false), 500),
            pace: boundedDisplayText(pace && pace.summary ? pace.summary : "", 500)
        }
        rows.push(row)
        return row
    }

    function rateWindowLabel(providerID, lane) {
        var key = providerKey(providerID)
        if (lane === "primary") {
            switch (key) {
            case "alibaba":
            case "opencode":
            case "opencodego":
                return i18n("5-hour")
            case "amp":
                return i18n("Amp Free")
            case "antigravity":
                return i18n("Gemini Models")
            case "azureopenai":
                return i18n("Status")
            case "bedrock":
                return i18n("Budget")
            case "commandcode":
            case "manus":
                return i18n("Monthly credits")
            case "copilot":
                return i18n("Premium")
            case "cursor":
                return i18n("Total")
            case "factory":
                return i18n("Standard")
            case "doubao":
            case "grok":
            case "groq":
            case "vertexai":
                return i18n("Requests")
            case "gemini":
                return i18n("Pro")
            case "kilo":
            case "kiro":
            case "mimo":
            case "warp":
            case "abacus":
                return i18n("Credits")
            case "kimi":
                return i18n("Weekly")
            case "minimax":
                return i18n("Prompts")
            case "openai":
                return i18n("Spend")
            case "openrouter":
                return i18n("API key limit")
            case "poe":
                return i18n("Points")
            case "zed":
                return i18n("Edit predictions")
            default:
                return i18n("Session")
            }
        }
        if (lane === "secondary") {
            switch (key) {
            case "antigravity":
                return i18n("Claude and GPT")
            case "amp":
                return i18n("Balance")
            case "azureopenai":
                return i18n("Deployment")
            case "bedrock":
                return i18n("Cost")
            case "copilot":
                return i18n("Chat")
            case "cursor":
                return i18n("Auto")
            case "factory":
                return i18n("Premium")
            case "doubao":
            case "kimi":
                return i18n("Rate limit")
            case "gemini":
                return i18n("Flash")
            case "grok":
                return i18n("On-demand")
            case "groq":
            case "vertexai":
                return i18n("Tokens")
            case "kilo":
                return i18n("Kilo Pass")
            case "kiro":
                return i18n("Bonus")
            case "mimo":
            case "minimax":
                return i18n("Window")
            case "openai":
                return i18n("Requests")
            case "warp":
                return i18n("Add-on credits")
            case "zed":
                return i18n("Billing cycle")
            default:
                return i18n("Weekly")
            }
        }
        if (lane === "tertiary") {
            if (key === "alibaba" || key === "opencodego") {
                return i18n("Monthly")
            }
            if (key === "claude") {
                return i18n("Sonnet")
            }
            if (key === "cursor") {
                return i18n("API")
            }
            if (key === "gemini") {
                return i18n("Flash Lite")
            }
            return i18n("Opus")
        }
        return i18n("Usage")
    }

    function providerCostSection(providerID, cost) {
        var key = providerKey(providerID)
        if (key === "manus" || key === "synthetic") {
            return null
        }

        if (!isCliRecord(cost)) {
            return null
        }

        var used = Number(cost.used)
        var limit = Number(cost.limit)
        var currency = boundedDisplayText(cost.currencyCode || "USD", 12)
        var period = boundedDisplayText(cost.period || i18n("This month"), 120)
        var hasUsed = isFinite(used)
        var hasLimit = isFinite(limit) && limit > 0
        if (!hasUsed) {
            return null
        }

        if (key === "factory" && period === "Extra usage balance") {
            return {
                title: i18n("Extra usage"),
                percentUsed: -1,
                spendLine: i18n("Balance: %1", amountString(used, currency)),
                percentLine: "",
                personalSpendLine: ""
            }
        }

        if (key === "opencodego" && period === "Zen balance") {
            return {
                title: i18n("Zen balance"),
                percentUsed: -1,
                spendLine: i18n("Balance: %1", amountString(used, currency)),
                percentLine: "",
                personalSpendLine: ""
            }
        }

        if (key === "minimax" && period === "MiniMax points balance") {
            return {
                title: i18n("Credits"),
                percentUsed: -1,
                spendLine: i18n("Balance: %1", Math.round(used)),
                percentLine: "",
                personalSpendLine: ""
            }
        }

        if (hasLimit) {
            var percent = clamp((used / limit) * 100, 0, 100)
            return {
                title: currency === "Quota" ? i18n("Quota usage") : i18n("Extra usage"),
                percentUsed: percent,
                spendLine: i18n("%1: %2 / %3", localizedPeriod(period), amountString(used, currency), amountString(limit, currency)),
                percentLine: i18n("%1% used", Math.round(percent)),
                personalSpendLine: cost.personalUsed && Number(cost.personalUsed) > 0
                    ? i18n("Your spend: %1", amountString(Number(cost.personalUsed), currency))
                    : ""
            }
        }

        if (key === "litellm") {
            return null
        }

        return {
            title: key === "openai" || key === "claude"
                ? i18n("API spend")
                : i18n("Extra usage"),
            percentUsed: -1,
            spendLine: i18n("%1: %2", localizedPeriod(period), amountString(used, currency)),
            percentLine: "",
            personalSpendLine: ""
        }
    }

    function resetCreditsSection(providerID, resetCredits) {
        if (providerKey(providerID) !== "codex" || !resetCredits) {
            return null
        }

        var count = Number(resetCredits.availableCount)
        if (!isFinite(count) || count <= 0) {
            return null
        }

        return {
            title: i18n("Reset credits"),
            line: i18np("%1 available", "%1 available", Math.round(count))
        }
    }

    function resetText(window, absolute) {
        if (!window.resetsAt) {
            return window.resetDescription && window.resetDescription.length > 0 ? window.resetDescription : ""
        }

        var date = new Date(window.resetsAt)
        if (isNaN(date.getTime())) {
            return String(window.resetsAt)
        }

        if (absolute === true) {
            return Qt.formatDateTime(date, "ddd HH:mm")
        }

        if (window.resetDescription && window.resetDescription.length > 0) {
            return window.resetDescription
        }

        var remainingMs = date.getTime() - Date.now()
        if (remainingMs <= 0) {
            return i18n("now")
        }
        var minutes = Math.max(1, Math.round(remainingMs / 60000))
        if (minutes < 60) {
            return i18np("%1 min", "%1 min", minutes)
        }
        var hours = Math.floor(minutes / 60)
        var restMinutes = minutes % 60
        if (hours < 24) {
            return restMinutes > 0 ? i18n("%1h %2m", hours, restMinutes) : i18np("%1h", "%1h", hours)
        }
        var days = Math.floor(hours / 24)
        var restHours = hours % 24
        return restHours > 0 ? i18n("%1d %2h", days, restHours) : i18np("%1d", "%1d", days)
    }

    function usageResetText(row) {
        if (!row) {
            return ""
        }
        if (row.resetsAt || row.resetDescription) {
            return resetText({
                resetsAt: row.resetsAt || "",
                resetDescription: row.resetDescription || ""
            }, resetTimesShowAbsolute)
        }
        return String(row.reset || "")
    }

    function statusText(status) {
        var indicator = String(status.indicator || "")
        var description = String(status.description || "").trim()
        if (indicator.length === 0 || indicator === "none") {
            return description
        }

        var labels = {
            "minor": i18n("Partial outage"),
            "major": i18n("Major outage"),
            "critical": i18n("Critical issue"),
            "maintenance": i18n("Maintenance"),
            "unknown": i18n("Status unknown")
        }
        var text = labels[indicator] || indicator
        return description.length > 0 ? text + ": " + description : text
    }

    function statusSeverity(status) {
        if (!status) {
            return ""
        }
        var indicator = String(status.indicator || "").toLowerCase()
        switch (indicator) {
        case "minor":
        case "maintenance":
        case "major":
        case "critical":
        case "unknown":
            return indicator
        default:
            return ""
        }
    }

    function statusBadgeColor(severity) {
        switch (String(severity || "")) {
        case "critical":
        case "major":
            return Kirigami.Theme.negativeTextColor
        case "minor":
        case "maintenance":
            return Kirigami.Theme.neutralTextColor
        case "unknown":
            return Kirigami.Theme.textColor
        default:
            return "transparent"
        }
    }

    function statusBadgeText(severity) {
        switch (String(severity || "")) {
        case "critical":
            return i18n("Critical")
        case "major":
            return i18n("Major")
        case "minor":
            return i18n("Issue")
        case "maintenance":
            return i18n("Maint.")
        case "unknown":
            return i18n("Unknown")
        default:
            return ""
        }
    }

    function statusMessageType(severity) {
        switch (String(severity || "")) {
        case "critical":
        case "major":
            return Kirigami.MessageType.Error
        case "minor":
        case "maintenance":
            return Kirigami.MessageType.Warning
        default:
            return Kirigami.MessageType.Information
        }
    }

    function primaryIncidentProvider() {
        var ranked = {
            "critical": 5,
            "major": 4,
            "minor": 3,
            "maintenance": 2,
            "unknown": 1
        }
        var best = null
        var bestRank = 0
        for (var i = 0; i < providers.length; i++) {
            var item = providers[i]
            var rank = item && item.statusSeverity ? ranked[item.statusSeverity] || 0 : 0
            if (rank > bestRank) {
                best = item
                bestRank = rank
            }
        }
        return best
    }

    function quotaWarningMarkers(row) {
        if (!showQuotaWarningMarkers || !row || !row.hasPercent) {
            return []
        }
        var warning = usageBarsShowUsed ? 80 : 20
        var critical = usageBarsShowUsed ? 95 : 5
        return [
            { percent: warning, severity: "minor" },
            { percent: critical, severity: "major" }
        ]
    }

    function resetNotificationMemo() {
        notificationMemo = ({})
        notificationsPrimed = false
        Qt.callLater(processNotifications)
    }

    function notificationProviderRefreshPending(providerID) {
        var key = providerMapKey(providerID)
        return key.length > 0 && notificationRefreshPending[key] === true
    }

    function setNotificationProviderRefreshPending(providerID, pending) {
        var key = providerMapKey(providerID)
        if (key.length === 0) {
            return
        }
        var nextPending = copyObject(notificationRefreshPending)
        if (pending) {
            nextPending[key] = true
        } else {
            delete nextPending[key]
        }
        notificationRefreshPending = nextPending
    }

    function markNotificationProvidersFresh(items) {
        var nextPending = copyObject(notificationRefreshPending)
        for (var i = 0; i < items.length; i++) {
            var item = items[i]
            if (!item || (item.error && String(item.error).length > 0)) {
                continue
            }
            var providerID = providerMapKey(item.provider)
            if (providerID.length === 0) {
                continue
            }
            var selectedAccount = selectedAccountForProvider(providerID)
            if (selectedAccount.length > 0 && accountLabel(item) !== selectedAccount) {
                continue
            }
            delete nextPending[providerID]
        }
        notificationRefreshPending = nextPending
    }

    function notificationScopeKey(item) {
        if (!item) {
            return JSON.stringify(["", ""])
        }
        var providerID = providerMapKey(item.provider)
        var selectedAccount = selectedAccountForProvider(providerID)
        var currentAccount = selectedAccount.length > 0 ? selectedAccount : accountLabel(item)
        return JSON.stringify([providerID, currentAccount])
    }

    function statusNotificationKey(item) {
        return "status:" + providerMapKey(item.provider)
    }

    function notificationScopePrimedKey(item) {
        return "scope:" + notificationScopeKey(item)
    }

    function clearNotificationScopeMemo(nextMemo, item) {
        var scope = notificationScopeKey(item)
        var quotaPrefix = "quota:" + scope + ":"
        var resetPrefix = "reset:" + scope + ":"
        for (var key in nextMemo) {
            if (key.indexOf(quotaPrefix) === 0 || key.indexOf(resetPrefix) === 0) {
                delete nextMemo[key]
            }
        }
    }

    function primeAccountNotificationScope(item, nextMemo) {
        nextMemo[notificationScopePrimedKey(item)] = "1"
        if (notifyQuotaWarnings) {
            var rows = item.rows || []
            for (var j = 0; j < rows.length; j++) {
                var level = quotaNotificationLevel(rows[j])
                if (level.length > 0) {
                    nextMemo[quotaNotificationKey(item, rows[j], j)] = level
                }
            }
        }
        if (notifyLimitResets) {
            // Arm rows that already sit at warning-level usage so a later
            // reset fires, but never fire on this first observation.
            var resetRows = item.rows || []
            for (var k = 0; k < resetRows.length; k++) {
                var resetRow = resetRows[k]
                if (resetRow && resetRow.hasPercent
                    && Number(resetRow.usedPercent) >= limitResetArmThreshold) {
                    nextMemo[limitResetNotificationKey(item, resetRow, k)] = "1"
                }
            }
        }
    }

    function primeNotifications() {
        var nextMemo = ({})
        for (var i = 0; i < providers.length; i++) {
            var item = providers[i]
            if (!item || notificationProviderRefreshPending(item.provider)) {
                continue
            }
            if (notifyStatusIncidents) {
                var statusValue = notificationStatusValue(item)
                if (statusValue.length > 0) {
                    nextMemo[statusNotificationKey(item)] = statusValue
                }
            }
            primeAccountNotificationScope(item, nextMemo)
        }
        notificationMemo = nextMemo
        notificationsPrimed = true
    }

    function processNotifications() {
        if (!enableNotifications || providers.length === 0) {
            return
        }
        if (!notificationsPrimed) {
            primeNotifications()
            return
        }

        var nextMemo = copyObject(notificationMemo)
        for (var i = 0; i < providers.length; i++) {
            var item = providers[i]
            if (!item) {
                continue
            }

            if (notificationProviderRefreshPending(item.provider)) {
                continue
            }
            if (notifyStatusIncidents) {
                processStatusNotification(item, nextMemo)
            }
            clearNotificationScopeMemo(nextMemo, item)
            if (notificationMemo[notificationScopePrimedKey(item)] !== "1") {
                primeAccountNotificationScope(item, nextMemo)
                continue
            }
            if (notifyQuotaWarnings) {
                processQuotaNotifications(item, nextMemo)
            }
            if (notifyLimitResets) {
                processLimitResetNotifications(item, nextMemo)
            }
        }
        notificationMemo = nextMemo
    }

    function processStatusNotification(item, nextMemo) {
        var key = statusNotificationKey(item)
        var value = notificationStatusValue(item)
        var previousValue = String(notificationMemo[key] || "")
        if (value.length > 0) {
            var previousSeverity = previousValue.length > 0 ? previousValue.split("|")[0] : ""
            var worsened = notificationRank(item.statusSeverity) > notificationRank(previousSeverity)
            // Notify for a new incident, worsened severity, or a changed
            // same-severity stable incident key so replacements are not missed
            // without treating provider-controlled status text as notification identity.
            var previousIncidentKey = notificationStatusIncidentKey(previousValue)
            var currentIncidentKey = notificationStatusIncidentKey(value)
            var incidentChanged = previousIncidentKey.length > 0
                && currentIncidentKey.length > 0
                && previousIncidentKey !== currentIncidentKey
            if (previousValue.length === 0 || worsened || incidentChanged) {
                sendPlasmaNotification(
                    i18n("%1 status issue", item.title),
                    item.status,
                    notificationUrgency(item.statusSeverity))
            }
            nextMemo[key] = value
        } else {
            delete nextMemo[key]
        }
    }

    function processQuotaNotifications(item, nextMemo) {
        var rows = item.rows || []
        for (var i = 0; i < rows.length; i++) {
            var row = rows[i]
            var key = quotaNotificationKey(item, row, i)
            var level = quotaNotificationLevel(row)
            var previousLevel = String(notificationMemo[key] || "")
            if (level.length > 0 && notificationRank(level) > notificationRank(previousLevel)) {
                var body = i18n("%1 is %2% used", row.label, Math.round(row.usedPercent))
                var resetLine = resetLabel(usageResetText(row))
                if (resetLine.length > 0) {
                    body += ". " + resetLine
                }
                sendPlasmaNotification(
                    level === "major" ? i18n("%1 quota critical", item.title) : i18n("%1 quota warning", item.title),
                    body,
                    notificationUrgency(level))
            }
            if (level.length > 0) {
                nextMemo[key] = level
            } else {
                delete nextMemo[key]
            }
        }
    }

    // Usage at or above this percent arms a row for reset detection; once armed,
    // dropping to or below the floor fires a single "limit reset" notification.
    // Mirrors the macOS weekly-limit reset detector, scoped to limits the user
    // was actually near so routine short-window resets stay quiet.
    readonly property int limitResetArmThreshold: 80
    readonly property int limitResetFloor: 5

    function processLimitResetNotifications(item, nextMemo) {
        var rows = item.rows || []
        for (var i = 0; i < rows.length; i++) {
            var row = rows[i]
            if (!row || !row.hasPercent) {
                continue
            }
            var used = Number(row.usedPercent)
            if (!isFinite(used)) {
                continue
            }
            var key = limitResetNotificationKey(item, row, i)
            var wasArmed = notificationMemo[key] === "1"
            if (wasArmed && used <= limitResetFloor) {
                sendPlasmaNotification(
                    i18n("%1 limit reset", item.title),
                    i18n("%1 is back to %2% used", row.label, Math.round(used)),
                    "low")
            } else if (used >= limitResetArmThreshold || (wasArmed && used > limitResetFloor)) {
                nextMemo[key] = "1"
            }
        }
    }

    function limitResetNotificationKey(item, row, index) {
        var lane = row && row.lane ? row.lane : ""
        var label = row && row.label ? row.label : ""
        return "reset:" + notificationScopeKey(item) + ":" + lane + ":" + label + ":" + index
    }

    function notificationStatusValue(item) {
        if (!item || !item.hasIncident || !item.statusSeverity || !item.status) {
            return ""
        }
        var incidentKey = item.statusIncidentKey ? String(item.statusIncidentKey) : ""
        return item.statusSeverity + "|" + incidentKey
    }

    function notificationStatusIncidentKey(value) {
        var text = String(value || "")
        var separator = text.indexOf("|")
        return separator >= 0 ? text.slice(separator + 1) : ""
    }

    function statusIncidentKey(status) {
        if (!status) {
            return ""
        }
        var keys = [
            "incidentId",
            "incident_id",
            "incidentID",
            "id"
        ]
        for (var i = 0; i < keys.length; i++) {
            var value = status[keys[i]]
            if (value !== null && value !== undefined && String(value).length > 0) {
                return String(value)
            }
        }
        var incident = status.incident || null
        if (incident && incident.id !== null && incident.id !== undefined && String(incident.id).length > 0) {
            return String(incident.id)
        }
        return ""
    }

    function quotaNotificationKey(item, row, index) {
        var lane = row && row.lane ? row.lane : ""
        var label = row && row.label ? row.label : ""
        return "quota:" + notificationScopeKey(item) + ":" + lane + ":" + label + ":" + index
    }

    function quotaNotificationLevel(row) {
        if (!row || !row.hasPercent) {
            return ""
        }
        var used = Number(row.usedPercent)
        if (!isFinite(used)) {
            return ""
        }
        if (used >= 95) {
            return "major"
        }
        if (used >= 80) {
            return "minor"
        }
        return ""
    }

    function notificationRank(severity) {
        switch (String(severity || "")) {
        case "critical":
            return 5
        case "major":
            return 4
        case "minor":
            return 3
        case "maintenance":
            return 2
        case "unknown":
            return 1
        default:
            return 0
        }
    }

    function notificationUrgency(severity) {
        switch (String(severity || "")) {
        case "critical":
        case "major":
            return "critical"
        case "unknown":
            return "low"
        default:
            return "normal"
        }
    }

    function sendPlasmaNotification(title, body, urgency) {
        var cleanTitle = String(title || "CodexBar").trim()
        var cleanBody = String(body || "").trim()
        var cleanUrgency = String(urgency || "normal").trim()
        if (cleanTitle.length === 0) {
            cleanTitle = "CodexBar"
        }
        if (cleanUrgency !== "low" && cleanUrgency !== "normal" && cleanUrgency !== "critical") {
            cleanUrgency = "normal"
        }
        var command = "if command -v notify-send >/dev/null 2>&1; then notify-send --app-name=CodexBar --icon=view-statistics --urgency="
            + shellQuote(cleanUrgency) + " -- " + shellQuote(cleanTitle) + " " + shellQuote(cleanBody) + "; fi"
        // A shell assignment cannot directly prefix the reserved word `if`.
        notificationSource.connectSource(commandWithRunNonce(":; " + command))
    }

    function updateScriptPath() {
        var url = Qt.resolvedUrl("../../scripts/update-widget.sh").toString()
        if (url.indexOf("file://") === 0) {
            return decodeURIComponent(url.substring(7))
        }
        return decodeURIComponent(url)
    }

    function buildUpdateCommand(installMode) {
        var scriptPath = updateScriptPath()
        var mode = installMode ? " --install" : " --check"
        var updateCommand = "if [ -x " + shellQuote(scriptPath) + " ]; then "
            + shellQuote(scriptPath) + mode
            + "; else printf '%s\\n' " + shellQuote(missingUpdateScriptJson()) + "; fi"
        return "sh -c " + shellQuote(updateCommand)
    }

    function missingUpdateScriptJson() {
        return JSON.stringify({
            status: "error",
            message: i18n("Widget updater script is missing from the installed package.")
        })
    }

    function updateCheckDue(forceCheck) {
        return UpdateLogic.updateCheckDue(
            updateChecksEnabled,
            autoUpdateLastCheck,
            autoUpdateIntervalHours,
            Date.now(),
            forceCheck === true)
    }

    function checkForWidgetUpdate(forceCheck) {
        if (connectedUpdateCommandSource.length > 0) {
            return
        }
        if (!updateCheckDue(forceCheck)) {
            scheduleNextUpdateCheck()
            return
        }
        updateCheckTimer.stop()
        setWidgetUpdateState(i18n("Checking for widget updates..."), "", false)
        connectedUpdateCommandSource = commandWithRunNonce(buildUpdateCommand(autoUpdateEnabled))
        updateSource.connectSource(connectedUpdateCommandSource)
        updateCommandTimeoutTimer.interval = autoUpdateEnabled
            ? widgetAutoUpdateTimeoutMs
            : widgetUpdateCheckTimeoutMs
        updateCommandTimeoutTimer.restart()
    }

    function scheduleNextUpdateCheck(lastCheckOverride) {
        updateCheckTimer.stop()
        if (!updateChecksEnabled || connectedUpdateCommandSource.length > 0) {
            return
        }
        var lastCheck = lastCheckOverride === undefined ? autoUpdateLastCheck : lastCheckOverride
        updateCheckTimer.interval = UpdateLogic.nextUpdateCheckDelay(
            updateChecksEnabled,
            lastCheck,
            autoUpdateIntervalHours,
            Date.now(),
            widgetUpdateMinimumTimerDelayMs)
        updateCheckTimer.restart()
    }

    function finishUpdateCommand(sourceName) {
        updateCommandTimeoutTimer.stop()
        updateSource.disconnectSource(sourceName)
        connectedUpdateCommandSource = ""
        var completedAt = new Date().toISOString()
        Plasmoid.configuration.autoUpdateLastCheck = completedAt
        scheduleNextUpdateCheck(completedAt)
    }

    function handleUpdateCommandTimeout() {
        if (connectedUpdateCommandSource.length === 0) {
            return
        }
        var sourceName = connectedUpdateCommandSource
        finishUpdateCommand(sourceName)
        setWidgetUpdateState(
            i18n("Widget update failed."),
            i18n("Widget update operation timed out."))
    }

    function setWidgetUpdateState(statusText, errorText, persistState) {
        updateStatusText = boundedWidgetUpdateText(statusText)
        updateErrorText = boundedWidgetUpdateText(errorText)
        if (persistState === false) {
            return
        }
        Plasmoid.configuration.widgetUpdateLastStatus = updateStatusText
        Plasmoid.configuration.widgetUpdateLastError = updateErrorText
    }

    function handleUpdateData(sourceName, stdoutText, stderrText) {
        if (sourceName !== connectedUpdateCommandSource) {
            return
        }
        finishUpdateCommand(sourceName)

        var trimmed = stdoutText.trim()
        if (trimmed.length === 0) {
            setWidgetUpdateState(
                i18n("Widget update check failed."),
                stderrText.trim().length > 0 ? boundedCliMessage(stderrText) : i18n("Widget update check returned no data."))
            return
        }

        var payload
        try {
            payload = JSON.parse(trimmed)
        } catch (error) {
            setWidgetUpdateState(
                i18n("Widget update check failed."),
                i18n("Could not parse widget update JSON: %1", error.message))
            return
        }

        processUpdateCheck(payload)
    }

    function processUpdateCheck(payload) {
        var status = String(payload && payload.status ? payload.status : "")
        var message = boundedCliMessage(payload && payload.message ? payload.message : "")
        var version = String(payload && payload.remoteVersion ? payload.remoteVersion : "")
        var url = String(payload && payload.assetUrl ? payload.assetUrl : "")

        if (status === "error") {
            setWidgetUpdateState(
                i18n("Widget update check failed."),
                message.length > 0 ? message : i18n("Widget update check failed."))
            return
        }

        if (status === "available") {
            var availableStatus = version.length > 0
                ? i18n("Widget update %1 is available.", version)
                : i18n("A widget update is available.")
            setWidgetUpdateState(availableStatus, "")
            if (!autoUpdateEnabled) {
                notifyAvailableUpdate(version, url)
            }
            return
        }
        if (status === "installed") {
            var restartText = i18n("Restart Plasma to apply the new widget version.")
            setWidgetUpdateState(version.length > 0
                ? i18n("Widget update %1 installed. %2", version, restartText)
                : i18n("Widget update installed. %1", restartText), "")
            notifyInstalledUpdate(version)
            return
        }
        if (status === "current") {
            setWidgetUpdateState(i18n("Widget is up to date."), "")
            return
        }
        if (status === "skipped") {
            setWidgetUpdateState(message.length > 0 ? message : i18n("Widget update skipped."), "")
            return
        }

        setWidgetUpdateState(
            i18n("Widget update check failed."),
            i18n("Unknown widget update status: %1", status))
    }

    function notifyAvailableUpdate(version, url) {
        if (!enableNotifications || !updateNotificationsEnabled) {
            return
        }
        var cleanVersion = String(version || "").trim()
        var memoKey = cleanVersion.length > 0 ? cleanVersion : url
        if (memoKey.length === 0 || memoKey === lastNotifiedUpdateVersion) {
            return
        }
        lastNotifiedUpdateVersion = memoKey
        Plasmoid.configuration.lastNotifiedUpdateVersion = memoKey
        var title = i18n("CodexBar widget update available")
        var body = cleanVersion.length > 0
            ? i18n("Version %1 is available.", cleanVersion)
            : i18n("A new widget version is available.")
        sendPlasmaNotification(title, body, "normal")
    }

    function notifyInstalledUpdate(version) {
        if (!enableNotifications || !updateNotificationsEnabled) {
            return
        }
        var cleanVersion = String(version || "").trim()
        var title = i18n("CodexBar widget update installed")
        var restartText = i18n("Restart Plasma to apply the new widget version.")
        var body = cleanVersion.length > 0
            ? i18n("Version %1 was installed. %2", cleanVersion, restartText)
            : i18n("A widget update was installed. %1", restartText)
        sendPlasmaNotification(title, body, "normal")
    }

    function planText(providerID, usage, item) {
        var identity = usage.identity || ({})
        var method = identity.loginMethod || usage.loginMethod || ""
        if (providerKey(providerID) === "codex" && method.length > 0) {
            return capitalize(method)
        }
        return ""
    }

    function providerKey(value) {
        var aliases = {
            "11labs": "elevenlabs",
            "abacus-ai": "abacus",
            "abacusai": "abacus",
            "agy": "antigravity",
            "ai&": "aiand",
            "ai-and": "aiand",
            "alibaba-coding-plan": "alibaba",
            "alibaba-token": "alibabatokenplan",
            "alibaba-token-plan": "alibabatokenplan",
            "aoai": "azureopenai",
            "ark": "doubao",
            "aws-bedrock": "bedrock",
            "azure-openai": "azureopenai",
            "bailian": "alibaba",
            "bailian-token-plan": "alibabatokenplan",
            "bob": "ibmbob",
            "bobshell": "ibmbob",
            "bytedance": "doubao",
            "chutes.ai": "chutes",
            "claw-router": "clawrouter",
            "cm": "crossmodel",
            "command-code": "commandcode",
            "crofai": "crof",
            "deep-infra": "deepinfra",
            "deep-seek": "deepseek",
            "dg": "deepgram",
            "di": "deepinfra",
            "droid": "factory",
            "ds": "deepseek",
            "eleven": "elevenlabs",
            "fw": "fireworks",
            "gemini-cli": "gemini",
            "groq-api": "groq",
            "groqcloud": "groq",
            "ibm-bob": "ibmbob",
            "kilo-ai": "kilo",
            "kimi-ai": "kimi",
            "kimi-k2": "kimik2",
            "kiro-cli": "kiro",
            "lc": "longcat",
            "litellm-proxy": "litellm",
            "llm-api-key-proxy": "llmproxy",
            "llm-proxy": "llmproxy",
            "long-cat": "longcat",
            "manicode": "codebuff",
            "mini-max": "minimax",
            "mistral-ai": "mistral",
            "neural": "neuralwatt",
            "notion-ai": "notion",
            "notionai": "notion",
            "nw": "neuralwatt",
            "openai-api": "openai",
            "or": "openrouter",
            "qwen": "qwencloud",
            "qwen-cloud": "qwencloud",
            "qwen-token-plan": "qwencloud",
            "sakana-ai": "sakana",
            "sf": "stepfun",
            "step-fun": "stepfun",
            "sub-2-api": "sub2api",
            "synthetic.new": "synthetic",
            "t3": "t3chat",
            "t3-chat": "t3chat",
            "ven": "venice",
            "zen-mux": "zenmux",
            "vertex": "vertexai",
            "volcengine": "doubao",
            "warp-ai": "warp",
            "warp-terminal": "warp",
            "wayfinder-router": "wayfinder",
            "xiaomi-mimo": "mimo",
            "z.ai": "zai"
        }
        return ProviderIdentity.providerKey(value, aliases)
    }

    function providerCliArgument(value) {
        switch (providerKey(value)) {
        case "abacus":
            return "abacusai"
        case "alibaba":
            return "alibaba-coding-plan"
        case "alibabatokenplan":
            return "alibaba-token-plan"
        case "azureopenai":
            return "azure-openai"
        case "groq":
            return "groqcloud"
        case "qwencloud":
            return "qwen-cloud"
        default:
            return providerKey(value)
        }
    }

    function providerTitle(value, displayName) {
        var key = providerKey(value)
        var preferred = String(displayName || "").trim()
        if (preferred.length > 0) {
            return preferred
        }

        var names = {
            "abacus": i18n("Abacus AI"),
            "aiand": i18n("ai&"),
            "alibaba": i18n("Alibaba"),
            "alibabatokenplan": i18n("Alibaba Token Plan"),
            "amp": i18n("Amp"),
            "antigravity": i18n("Antigravity"),
            "augment": i18n("Augment"),
            "azureopenai": i18n("Azure OpenAI"),
            "bedrock": i18n("AWS Bedrock"),
            "chutes": i18n("Chutes"),
            "claude": i18n("Claude"),
            "clawrouter": i18n("ClawRouter"),
            "clinepass": i18n("ClinePass"),
            "codebuff": i18n("Codebuff"),
            "codex": i18n("Codex"),
            "commandcode": i18n("Command Code"),
            "copilot": i18n("Copilot"),
            "crof": i18n("Crof"),
            "crossmodel": i18n("CrossModel"),
            "cursor": i18n("Cursor"),
            "deepgram": i18n("Deepgram"),
            "deepinfra": i18n("DeepInfra"),
            "deepseek": i18n("DeepSeek"),
            "devin": i18n("Devin"),
            "doubao": i18n("Doubao"),
            "elevenlabs": i18n("ElevenLabs"),
            "factory": i18n("Droid"),
            "fireworks": i18n("Fireworks"),
            "gemini": i18n("Gemini"),
            "grok": i18n("Grok"),
            "groq": i18n("Groq"),
            "ibmbob": i18n("IBM Bob"),
            "jetbrains": i18n("JetBrains AI"),
            "kilo": i18n("Kilo"),
            "kimi": i18n("Kimi Code"),
            "kimik2": i18n("Kimi K2 (unofficial)"),
            "kiro": i18n("Kiro"),
            "litellm": i18n("LiteLLM"),
            "llmproxy": i18n("LLM Proxy"),
            "longcat": i18n("LongCat"),
            "manus": i18n("Manus"),
            "mimo": i18n("Xiaomi MiMo"),
            "minimax": i18n("MiniMax"),
            "mistral": i18n("Mistral"),
            "moonshot": i18n("Moonshot / Kimi Open Platform"),
            "neuralwatt": i18n("Neuralwatt"),
            "notion": i18n("Notion AI"),
            "ollama": i18n("Ollama"),
            "openai": i18n("OpenAI"),
            "opencode": i18n("OpenCode"),
            "opencodego": i18n("OpenCode Go"),
            "openrouter": i18n("OpenRouter"),
            "perplexity": i18n("Perplexity"),
            "poe": i18n("Poe"),
            "qoder": i18n("Qoder"),
            "qwencloud": i18n("Qwen Cloud"),
            "sakana": i18n("Sakana AI"),
            "stepfun": i18n("StepFun"),
            "sub2api": i18n("sub2api"),
            "synthetic": i18n("Synthetic"),
            "t3chat": i18n("T3 Chat"),
            "venice": i18n("Venice"),
            "vertexai": i18n("Vertex AI"),
            "warp": i18n("Warp"),
            "wayfinder": i18n("Wayfinder"),
            "windsurf": i18n("Windsurf"),
            "xai": i18n("xAI"),
            "zai": i18n("z.ai / GLM"),
            "zed": i18n("Zed"),
            "zenmux": i18n("ZenMux"),
            "zoommate": i18n("ZoomMate")
        }

        if (hasOwnKey(names, key)) {
            return names[key]
        }

        var words = String(key).replace(/[_-]/g, " ").split(" ")
        for (var i = 0; i < words.length; i++) {
            if (words[i].length > 0) {
                words[i] = words[i].charAt(0).toUpperCase() + words[i].slice(1)
            }
        }
        return words.join(" ")
    }

    function providerIconSource(value) {
        var key = ProviderIdentity.providerMapKey(providerKey(value))
        if (!/^[a-z0-9][a-z0-9._-]*$/.test(key) || key.indexOf("..") !== -1) {
            return "view-statistics"
        }
        // Keyed by the resolved providerKey, so CLI aliases such as
        // "aws-bedrock" or "kimi-k2" are already normalized before this lookup.
        // Only providers whose icon asset name differs from their key belong here.
        var aliases = {
            "gemini": "gemini-white.png"
        }
        key = ProviderIdentity.providerKey(key, aliases)
        var fileName = key.indexOf(".") === -1 ? key + ".svg" : key
        return Qt.resolvedUrl("../icons/providers/" + fileName)
    }

    function providerIconIsMask(value) {
        return true
    }

    function providerColor(value) {
        switch (providerKey(value)) {
        case "aiand":
            return Qt.rgba(226 / 255, 92 / 255, 43 / 255, 1)
        case "codex":
            return Qt.rgba(73 / 255, 163 / 255, 176 / 255, 1)
        case "openai":
            return Qt.rgba(0.06, 0.51, 0.43, 1)
        case "azureopenai":
            return Qt.rgba(0, 120 / 255, 212 / 255, 1)
        case "claude":
            return Qt.rgba(204 / 255, 124 / 255, 94 / 255, 1)
        case "clinepass":
            return Qt.rgba(0.38, 0.64, 0.98, 1)
        case "cursor":
            return Qt.rgba(0, 191 / 255, 165 / 255, 1)
        case "opencode":
            return Qt.rgba(59 / 255, 130 / 255, 246 / 255, 1)
        case "opencodego":
            return Qt.rgba(59 / 255, 130 / 255, 246 / 255, 1)
        case "alibaba":
            return Qt.rgba(1, 106 / 255, 0, 1)
        case "alibabatokenplan":
            return Qt.rgba(1, 106 / 255, 0, 1)
        case "factory":
            return Qt.rgba(1, 107 / 255, 53 / 255, 1)
        case "fireworks":
            return Qt.rgba(242 / 255, 91 / 255, 28 / 255, 1)
        case "gemini":
            return Qt.rgba(171 / 255, 135 / 255, 234 / 255, 1)
        case "antigravity":
            return Qt.rgba(96 / 255, 186 / 255, 126 / 255, 1)
        case "copilot":
            return Qt.rgba(168 / 255, 85 / 255, 247 / 255, 1)
        case "devin":
            return Qt.rgba(70 / 255, 180 / 255, 130 / 255, 1)
        case "minimax":
            return Qt.rgba(254 / 255, 96 / 255, 60 / 255, 1)
        case "manus":
            return Qt.rgba(52 / 255, 50 / 255, 45 / 255, 1)
        case "kimi":
            return Qt.rgba(254 / 255, 96 / 255, 60 / 255, 1)
        case "kilo":
            return Qt.rgba(242 / 255, 112 / 255, 39 / 255, 1)
        case "kiro":
            return Qt.rgba(1, 153 / 255, 0, 1)
        case "vertexai":
            return Qt.rgba(66 / 255, 133 / 255, 244 / 255, 1)
        case "augment":
            return Qt.rgba(99 / 255, 102 / 255, 241 / 255, 1)
        case "jetbrains":
            return Qt.rgba(1, 51 / 255, 153 / 255, 1)
        case "kimik2":
            return Qt.rgba(76 / 255, 0, 1, 1)
        case "moonshot":
            return Qt.rgba(32 / 255, 93 / 255, 235 / 255, 1)
        case "amp":
            return Qt.rgba(220 / 255, 38 / 255, 38 / 255, 1)
        case "t3chat":
            return Qt.rgba(245 / 255, 102 / 255, 71 / 255, 1)
        case "ollama":
            return Qt.rgba(136 / 255, 136 / 255, 136 / 255, 1)
        case "synthetic":
            return Qt.rgba(20 / 255, 20 / 255, 20 / 255, 1)
        case "warp":
            return Qt.rgba(147 / 255, 139 / 255, 180 / 255, 1)
        case "openrouter":
            return Qt.rgba(100 / 255, 103 / 255, 242 / 255, 1)
        case "elevenlabs":
            return Qt.rgba(0.92, 0.92, 0.90, 1)
        case "windsurf":
            return Qt.rgba(52 / 255, 232 / 255, 187 / 255, 1)
        case "zed":
            return Qt.rgba(8 / 255, 78 / 255, 1, 1)
        case "perplexity":
            return Qt.rgba(32 / 255, 178 / 255, 170 / 255, 1)
        case "qoder":
            return Qt.rgba(16 / 255, 185 / 255, 129 / 255, 1)
        case "sakana":
            return Qt.rgba(0.16, 0.46, 0.86, 1)
        case "mimo":
            return Qt.rgba(1, 105 / 255, 0, 1)
        case "doubao":
            return Qt.rgba(51 / 255, 112 / 255, 1, 1)
        case "abacus":
            return Qt.rgba(56 / 255, 189 / 255, 248 / 255, 1)
        case "mistral":
            return Qt.rgba(1, 80 / 255, 15 / 255, 1)
        case "deepseek":
            return Qt.rgba(0.32, 0.49, 0.94, 1)
        case "codebuff":
            return Qt.rgba(68 / 255, 1, 0, 1)
        case "crof":
            return Qt.rgba(0.18, 0.67, 0.58, 1)
        case "crossmodel":
            return Qt.rgba(124 / 255, 58 / 255, 237 / 255, 1)
        case "venice":
            return Qt.rgba(0.2, 0.6, 1, 1)
        case "commandcode":
            return Qt.rgba(160 / 255, 77 / 255, 253 / 255, 1)
        case "clawrouter":
            return Qt.rgba(89 / 255, 110 / 255, 246 / 255, 1)
        case "stepfun":
            return Qt.rgba(0.13, 0.59, 0.95, 1)
        case "bedrock":
            return Qt.rgba(1, 0.6, 0, 1)
        case "grok":
        case "wayfinder":
            return Qt.rgba(16 / 255, 163 / 255, 127 / 255, 1)
        case "groq":
            return Qt.rgba(245 / 255, 104 / 255, 68 / 255, 1)
        case "ibmbob":
            return Qt.rgba(14 / 255, 97 / 255, 250 / 255, 1)
        case "llmproxy":
            return Qt.rgba(36 / 255, 180 / 255, 126 / 255, 1)
        case "litellm":
            return Qt.rgba(76 / 255, 137 / 255, 240 / 255, 1)
        case "deepgram":
            return Qt.rgba(100 / 255, 103 / 255, 242 / 255, 1)
        case "deepinfra":
            return Qt.rgba(42 / 255, 50 / 255, 117 / 255, 1)
        case "poe":
            return Qt.rgba(93 / 255, 92 / 255, 222 / 255, 1)
        case "chutes":
            return Qt.rgba(49 / 255, 132 / 255, 1, 1)
        case "longcat":
            return Qt.rgba(1, 209 / 255, 0, 1)
        case "neuralwatt":
            return Qt.rgba(0.22, 0.85, 0.55, 1)
        case "notion":
            return Qt.rgba(51 / 255, 126 / 255, 169 / 255, 1)
        case "qwencloud":
            return Qt.rgba(97 / 255, 92 / 255, 237 / 255, 1)
        case "sub2api":
            return Qt.rgba(45 / 255, 198 / 255, 216 / 255, 1)
        case "xai":
            return Qt.rgba(142 / 255, 142 / 255, 147 / 255, 1)
        case "zenmux":
            return Qt.rgba(108 / 255, 92 / 255, 231 / 255, 1)
        case "zoommate":
            return Qt.rgba(11 / 255, 92 / 255, 1, 1)
        case "zai":
            return Qt.rgba(232 / 255, 90 / 255, 106 / 255, 1)
        default:
            return Kirigami.Theme.highlightColor
        }
    }

    function providerDashboardUrl(providerID) {
        switch (providerKey(providerID)) {
        case "abacus":
            return "https://apps.abacus.ai/chatllm/admin/compute-points-usage"
        case "alibaba":
            return "https://modelstudio.console.alibabacloud.com/ap-southeast-1/?tab=coding-plan#/efm/coding_plan"
        case "alibabatokenplan":
            return "https://bailian.console.aliyun.com/cn-beijing?tab=plan#/efm/subscription/token-plan"
        case "aiand":
            return "https://console.aiand.com"
        case "amp":
            return "https://ampcode.com/settings/usage"
        case "augment":
            return "https://app.augmentcode.com/account/subscription"
        case "azureopenai":
            return "https://ai.azure.com"
        case "bedrock":
            return "https://console.aws.amazon.com/bedrock"
        case "chutes":
            return "https://chutes.ai"
        case "clinepass":
            return "https://app.cline.bot/dashboard/subscription?personal=true"
        case "codebuff":
            return "https://www.codebuff.com/usage"
        case "clawrouter":
            return "https://clawrouter.openclaw.ai/dashboard/access"
        case "commandcode":
            return "https://commandcode.ai/studio"
        case "crof":
            return "https://crof.ai/dashboard"
        case "crossmodel":
            return "https://crossmodel.ai/console/usage"
        case "codex":
            return "https://chatgpt.com/codex/settings/usage"
        case "claude":
            return "https://console.anthropic.com/settings/billing"
        case "copilot":
            return "https://github.com/settings/copilot"
        case "cursor":
            return "https://cursor.com/dashboard?tab=usage"
        case "deepgram":
            return "https://console.deepgram.com/project/"
        case "deepinfra":
            return "https://deepinfra.com/dash"
        case "deepseek":
            return "https://platform.deepseek.com/usage"
        case "devin":
            return "https://app.devin.ai"
        case "doubao":
            return "https://console.volcengine.com/ark/region:ark+cn-beijing/openManagement?LLM=%7B%7D&advancedActiveKey=subscribe"
        case "elevenlabs":
            return "https://elevenlabs.io/app/developers/usage"
        case "factory":
            return "https://app.factory.ai/settings/billing"
        case "fireworks":
            return "https://app.fireworks.ai"
        case "gemini":
            return "https://gemini.google.com"
        case "grok":
            return "https://grok.com/?_s=usage"
        case "groq":
            return "https://console.groq.com/dashboard/usage"
        case "ibmbob":
            return "https://bob.ibm.com"
        case "kilo":
            return "https://app.kilo.ai/usage"
        case "kimi":
            return "https://www.kimi.com/code/console"
        case "kiro":
            return "https://app.kiro.dev/account/usage"
        case "longcat":
            return "https://longcat.chat/platform/"
        case "manus":
            return "https://manus.im"
        case "mimo":
            return "https://platform.xiaomimimo.com/#/console/balance"
        case "mistral":
            return "https://admin.mistral.ai/organization/usage"
        case "moonshot":
            return "https://platform.moonshot.ai/console/account"
        case "neuralwatt":
            return "https://portal.neuralwatt.com/dashboard"
        case "minimax":
            return "https://platform.minimax.io/user-center/payment/coding-plan?cycle_type=3"
        case "ollama":
            return "https://ollama.com/settings"
        case "notion":
            return "https://app.notion.com/"
        case "openai":
            return "https://platform.openai.com/usage"
        case "opencode":
        case "opencodego":
            return "https://opencode.ai/auth"
        case "openrouter":
            return "https://openrouter.ai/settings/credits"
        case "perplexity":
            return "https://www.perplexity.ai/account/usage"
        case "poe":
            return "https://poe.com/api/keys"
        case "qoder":
            return "https://qoder.com/account/usage"
        case "qwencloud":
            return "https://home.qwencloud.com/billing/subscription/token-plan-individual"
        case "sakana":
            return "https://console.sakana.ai/billing"
        case "stepfun":
            return "https://platform.stepfun.com/plan-usage"
        case "t3chat":
            return "https://t3.chat/settings/customization"
        case "venice":
            return "https://venice.ai/settings/api"
        case "vertexai":
            return "https://console.cloud.google.com/vertex-ai"
        case "warp":
            return "https://docs.warp.dev/reference/cli/api-keys"
        case "wayfinder":
            return "http://127.0.0.1:8088/router"
        case "windsurf":
            return "https://windsurf.com/subscription/usage"
        case "xai":
            return "https://console.x.ai"
        case "zenmux":
            return "https://zenmux.ai/platform/management"
        case "zoommate":
            return "https://zoommate.zoom.us/#/?settings=credit-usage"
        case "zai":
            return "https://z.ai/manage-apikey/coding-plan/personal/my-plan"
        default:
            return ""
        }
    }

    function providerDocsUrl(providerID) {
        var key = providerKey(providerID)
        var docs = {
            abacus: "abacus.md",
            aiand: "aiand.md",
            alibaba: "alibaba-coding-plan.md",
            alibabatokenplan: "alibaba-token-plan.md",
            amp: "amp.md",
            antigravity: "antigravity.md",
            augment: "augment.md",
            azureopenai: "providers.md#azure-openai",
            bedrock: "bedrock.md",
            chutes: "chutes.md",
            clawrouter: "clawrouter.md",
            claude: "claude.md",
            codebuff: "codebuff.md",
            commandcode: "command-code.md",
            codex: "codex.md",
            copilot: "copilot.md",
            crof: "crof.md",
            crossmodel: "crossmodel.md",
            cursor: "cursor.md",
            deepgram: "deepgram.md",
            deepinfra: "deepinfra.md",
            deepseek: "deepseek.md",
            devin: "devin.md",
            doubao: "doubao.md",
            elevenlabs: "elevenlabs.md",
            factory: "factory.md",
            fireworks: "fireworks.md",
            gemini: "gemini.md",
            grok: "grok.md",
            groq: "groqcloud.md",
            ibmbob: "ibm-bob.md",
            jetbrains: "jetbrains.md",
            kilo: "kilo.md",
            kimi: "kimi.md",
            kimik2: "kimi-k2.md",
            kiro: "kiro.md",
            litellm: "litellm.md",
            llmproxy: "llm-proxy.md",
            manus: "manus.md",
            mimo: "mimo.md",
            mistral: "providers.md#mistral",
            minimax: "minimax.md",
            moonshot: "moonshot.md",
            neuralwatt: "neuralwatt.md",
            notion: "notion.md",
            ollama: "ollama.md",
            opencode: "opencode.md",
            opencodego: "opencode.md",
            openai: "openai.md",
            openrouter: "openrouter.md",
            perplexity: "providers.md#perplexity",
            poe: "poe.md",
            qoder: "qoder.md",
            qwencloud: "qwen-cloud.md",
            sakana: "sakana.md",
            stepfun: "stepfun.md",
            synthetic: "providers.md#synthetic",
            sub2api: "sub2api.md",
            t3chat: "providers.md#t3-chat",
            venice: "venice.md",
            vertexai: "vertexai.md",
            warp: "warp.md",
            wayfinder: "wayfinder.md",
            windsurf: "windsurf.md",
            xai: "xai.md",
            zenmux: "zenmux.md",
            zoommate: "zoommate.md",
            zai: "zai.md",
            zed: "zed.md"
        }
        if (!hasOwnKey(docs, key)) {
            return ""
        }
        return "https://github.com/steipete/CodexBar/blob/main/docs/" + docs[key]
    }

    function providerLoginUrl(providerID) {
        switch (providerKey(providerID)) {
        case "codex":
        case "openai":
            return "https://chatgpt.com"
        case "claude":
            return "https://claude.ai"
        case "cursor":
            return "https://cursor.com/settings"
        case "opencode":
        case "opencodego":
            return "https://opencode.ai/auth"
        case "gemini":
            return "https://aistudio.google.com"
        case "factory":
            return "https://app.factory.ai"
        case "copilot":
            return "https://github.com/login"
        case "devin":
            return "https://app.devin.ai/settings/usage"
        case "manus":
            return "https://manus.im"
        case "mimo":
            return "https://platform.xiaomimimo.com/api/v1/genLoginUrl?currentPath=%2F%23%2Fconsole%2Fbalance"
        case "perplexity":
            return "https://www.perplexity.ai"
        default:
            return ""
        }
    }

    function providerStatusUrl(providerID) {
        switch (providerKey(providerID)) {
        case "alibaba":
        case "alibabatokenplan":
            return "https://status.aliyun.com"
        case "antigravity":
        case "gemini":
            return "https://www.google.com/appsstatus/dashboard/products/npdyhgECDJ6tB66MxXyo/history"
        case "azureopenai":
            return "https://azure.status.microsoft/en-us/status"
        case "augment":
            return "https://status.augmentcode.com"
        case "bedrock":
        case "kiro":
            return "https://health.aws.amazon.com/health/status"
        case "codex":
        case "openai":
            return "https://status.openai.com/"
        case "claude":
            return "https://status.claude.com/"
        case "copilot":
            return "https://www.githubstatus.com/"
        case "cursor":
            return "https://status.cursor.com"
        case "deepgram":
            return "https://status.deepgram.com"
        case "deepinfra":
            return "https://status.deepinfra.com"
        case "deepseek":
            return "https://status.deepseek.com"
        case "elevenlabs":
            return "https://status.elevenlabs.io"
        case "factory":
            return "https://status.factory.ai"
        case "grok":
            return "https://status.x.ai"
        case "groq":
            return "https://status.groq.com"
        case "ibmbob":
            return "https://status.bob.ibm.com"
        case "mistral":
            return "https://status.mistral.ai"
        case "openrouter":
            return "https://status.openrouter.ai/"
        case "notion":
            return "https://status.notion.so/"
        case "perplexity":
            return "https://status.perplexity.com/"
        case "qwencloud":
            return "https://status.alibabacloud.com"
        case "vertexai":
            return "https://status.cloud.google.com"
        case "xai":
            return "https://status.x.ai"
        case "zoommate":
            return "https://www.zoomstatus.com/"
        default:
            return ""
        }
    }

    function httpsUrlHost(url) {
        var match = String(url || "").trim().match(/^https:\/\/([^\/?#]+)/i)
        return match ? match[1].toLowerCase() : ""
    }

    function safeStatusUrl(providerID, url) {
        var fallback = providerStatusUrl(providerID)
        var fallbackHost = httpsUrlHost(fallback)
        var candidate = String(url || "").trim()
        var candidateHost = httpsUrlHost(candidate)
        if (fallbackHost.length === 0) {
            return ""
        }
        if (candidateHost.length === 0) {
            return fallback
        }
        return candidateHost === fallbackHost ? candidate : fallback
    }

    function providerChangelogUrl(providerID) {
        switch (providerKey(providerID)) {
        case "codex":
            return "https://github.com/openai/codex/releases"
        case "claude":
            return "https://github.com/anthropics/claude-code/releases"
        case "gemini":
            return "https://github.com/google-gemini/gemini-cli/releases"
        case "grok":
            return "https://x.ai/news"
        default:
            return ""
        }
    }

    function actionRows(item) {
        if (!item) {
            return []
        }

        var rows = []
        rows.push({
            title: accountLoadingForProvider(item.provider) ? i18n("Loading accounts...") : i18n("Accounts..."),
            icon: "user-identity",
            action: "accounts",
            enabled: !accountLoadingForProvider(item.provider)
        })

        var accountAction = providerAccountAction(item)
        if (accountAction) {
            rows.push(accountAction)
        }

        if (item.dashboardUrl && item.dashboardUrl.length > 0) {
            rows.push({ title: i18n("Usage Dashboard"), icon: "view-statistics", action: "dashboard", enabled: true })
        }
        if (safeStatusUrl(item.provider, item.statusUrl).length > 0) {
            rows.push({ title: i18n("Status Page"), icon: "network-connect", action: "status", enabled: true })
        }
        if (showProviderChangelogs && item.changelogUrl && item.changelogUrl.length > 0) {
            rows.push({ title: i18n("Changelog"), icon: "view-list-details", action: "changelog", enabled: true })
        }
        var docsUrl = providerDocsUrl(item.provider)
        if (docsUrl.length > 0) {
            rows.push({ title: i18n("Docs"), icon: "help-contents", action: "docs", url: docsUrl, enabled: true })
        }

        rows.push({ title: i18n("Refresh"), icon: "view-refresh", action: "refresh", enabled: true, separatorBefore: true })
        rows.push({ title: i18n("Settings..."), icon: "configure", action: "settings", enabled: true })
        rows.push({ title: i18n("About CodexBar"), icon: "help-about", action: "about", enabled: true })
        return rows
    }

    function providerAccountAction(item) {
        var title = item.account && item.account.length > 0 ? i18n("Switch Account...") : i18n("Add Account...")
        var loginUrl = providerLoginUrl(item.provider)
        switch (providerKey(item.provider)) {
        case "devin":
            return { title: i18n("Open Devin..."), icon: "internet-services", action: "account-url", url: "https://app.devin.ai/settings/usage", enabled: true }
        case "factory":
            return { title: i18n("Open Droid in Browser..."), icon: "internet-services", action: "account-url", url: "https://app.factory.ai", enabled: true }
        case "manus":
            return { title: title, icon: "internet-services", action: "account-url", url: "https://manus.im", enabled: true }
        case "mimo":
            return { title: title, icon: "internet-services", action: "account-url", url: "https://platform.xiaomimimo.com/api/v1/genLoginUrl?currentPath=%2F%23%2Fconsole%2Fbalance", enabled: true }
        case "perplexity":
            return { title: title, icon: "internet-services", action: "account-url", url: "https://www.perplexity.ai/", enabled: true }
        default:
            return loginUrl.length > 0
                ? { title: title, icon: "internet-services", action: "account-url", url: loginUrl, enabled: true }
                : null
        }
    }

    function performAction(actionRow) {
        var actionID = actionRow && actionRow.action ? actionRow.action : actionRow
        var item = selectedProviderData
        if (actionID === "dashboard" && item) {
            Qt.openUrlExternally(item.dashboardUrl)
        } else if (actionID === "status" && item) {
            Qt.openUrlExternally(safeStatusUrl(item.provider, item.statusUrl))
        } else if (actionID === "changelog" && item) {
            Qt.openUrlExternally(item.changelogUrl)
        } else if (actionID === "docs" && actionRow && actionRow.url) {
            Qt.openUrlExternally(actionRow.url)
        } else if (actionID === "accounts" && item) {
            root.loadAccounts(item.provider)
        } else if (actionID === "account-url" && actionRow && actionRow.url) {
            Qt.openUrlExternally(actionRow.url)
        } else if (actionID === "refresh") {
            root.refreshNow()
        } else if (actionID === "about") {
            Qt.openUrlExternally("https://github.com/steipete/CodexBar")
        } else if (actionID === "settings") {
            var action = Plasmoid.internalAction("configure")
            if (action) {
                action.trigger()
            }
        }
    }

    function withAlpha(color, alpha) {
        return Qt.rgba(color.r, color.g, color.b, alpha)
    }

    function canvasColor(color, alpha) {
        var opacity = alpha === undefined ? color.a : alpha
        return "rgba("
            + Math.round(color.r * 255) + ", "
            + Math.round(color.g * 255) + ", "
            + Math.round(color.b * 255) + ", "
            + opacity + ")"
    }

    function contrastTextColor(color) {
        var luminance = (0.2126 * color.r) + (0.7152 * color.g) + (0.0722 * color.b)
        return luminance > 0.62 ? Qt.rgba(0.08, 0.08, 0.1, 1) : Qt.rgba(1, 1, 1, 1)
    }

    function readableAccentColor(accent, background) {
        var surface = background || Kirigami.Theme.backgroundColor
        return ThemeContrast.readableAccentColor(
            accent,
            surface,
            Kirigami.Theme.textColor)
    }

    function providerReadableColor(value, background) {
        return readableAccentColor(
            providerColor(value),
            background || Kirigami.Theme.backgroundColor)
    }

    function copyObject(item) {
        var copy = ({})
        for (var key in item) {
            if (!hasOwnKey(item, key) || isUnsafeObjectKey(key)) {
                continue
            }
            copy[key] = item[key]
        }
        return copy
    }

    function hasText(value) {
        return String(value || "").trim().length > 0
    }

    function hasAdditionalSections(item) {
        return item && (item.credits !== null || item.resetCredits || item.usageDashboard || item.providerCost || item.tokenCost) ? true : false
    }

    function capitalize(value) {
        var text = String(value || "")
        if (text.length === 0) {
            return ""
        }
        return text.charAt(0).toUpperCase() + text.slice(1)
    }

    function localizedPeriod(value) {
        var text = String(value || "").trim()
        switch (text.toLowerCase()) {
        case "last 30 days":
            return i18n("Last 30 days")
        case "this month":
            return i18n("This month")
        case "today":
            return i18n("Today")
        default:
            return text
        }
    }

    function amountString(value, currency) {
        if (currency === "Quota") {
            return Math.round(value).toString()
        }
        var numeric = Number(value)
        var negative = numeric < 0
        var amount = Math.abs(numeric).toFixed(2)
        if (currency === "USD") {
            return negative ? "-$" + amount : "$" + amount
        }
        return (negative ? "-" : "") + currency + " " + amount
    }

    function costLine(label, cost, tokens, currency) {
        var costValue = isFinite(Number(cost)) ? amountString(Number(cost), currency) : "-"
        if (isFinite(Number(tokens))) {
            return i18n("%1: %2 - %3 tokens", label, costValue, tokenCountString(Number(tokens)))
        }
        return i18n("%1: %2", label, costValue)
    }

    function tokenCountString(tokens) {
        var value = Number(tokens)
        if (!isFinite(value)) {
            return "-"
        }
        var absValue = Math.abs(value)
        var sign = value < 0 ? "-" : ""
        if (absValue >= 1000000000) {
            return sign + scaledTokenCount(absValue / 1000000000) + "B"
        }
        if (absValue >= 1000000) {
            return sign + scaledTokenCount(absValue / 1000000) + "M"
        }
        if (absValue >= 1000) {
            return sign + scaledTokenCount(absValue / 1000) + "K"
        }
        return Math.round(value).toString()
    }

    function scaledTokenCount(value) {
        if (value >= 10) {
            return Number(value).toFixed(0)
        }
        var text = Number(value).toFixed(1)
        return text.replace(/\.0$/, "")
    }

    function tokenCostHint(providerID) {
        switch (providerKey(providerID)) {
        case "codex":
            return i18n("Estimated from local Codex logs for the selected account.")
        case "claude":
            return i18n("Estimated from local Claude logs.")
        default:
            return ""
        }
    }

    function firstUsageRow(item) {
        if (!item || !item.rows) {
            return null
        }
        for (var i = 0; i < item.rows.length; i++) {
            if (item.rows[i] && item.rows[i].hasPercent) {
                return item.rows[i]
            }
        }
        return null
    }

    function usageRowForLane(item, lane) {
        if (!item || !item.rows) {
            return null
        }
        for (var i = 0; i < item.rows.length; i++) {
            if (item.rows[i] && item.rows[i].lane === lane && item.rows[i].hasPercent) {
                return item.rows[i]
            }
        }
        return null
    }

    function switcherMetricRow(item) {
        if (!item || !item.rows || item.rows.length === 0) {
            return null
        }

        var key = providerKey(item.provider)
        var primary = usageRowForLane(item, "primary")
        var secondary = usageRowForLane(item, "secondary")
        if (key === "factory") {
            return secondary || primary || firstUsageRow(item)
        }
        if (key === "perplexity") {
            if (primary && primary.leftPercent > 0) {
                return primary
            }
            return secondary || usageRowForLane(item, "tertiary") || primary || firstUsageRow(item)
        }
        if (key === "cursor" && primary && primary.leftPercent <= 0
                && item.providerCost && item.providerCost.percentUsed >= 0) {
            var used = clamp(Number(item.providerCost.percentUsed), 0, 100)
            return {
                lane: "providerCost",
                label: i18n("Included plan"),
                hasPercent: true,
                usedPercent: used,
                leftPercent: clamp(100 - used, 0, 100),
                pacePercent: -1,
                paceOnTop: true,
                reset: "",
                pace: ""
            }
        }

        return primary || secondary || firstUsageRow(item)
    }

    function switcherPercent(item) {
        var row = switcherMetricRow(item)
        return row ? displayPercent(row) : -1
    }

    function isOverviewErrorOnly(item) {
        return item
            && item.error
            && item.error.length > 0
            && (!item.rows || item.rows.length === 0)
            && providerPlaceholderText(item).length === 0
            && item.credits === null
            && !item.resetCredits
            && !item.providerCost
            && !item.tokenCost
    }

    function overviewProviders() {
        var eligible = []
        if (!providers) {
            return eligible
        }
        for (var i = 0; i < providers.length; i++) {
            if (!isOverviewErrorOnly(providers[i])) {
                eligible.push(providers[i])
            }
        }

        var configured = configuredOverviewProviderIDs()
        if (String(overviewProviderIDsRaw || "").trim().length === 0) {
            return eligible.slice(0, maxOverviewProviders)
        }
        if (configured.length === 0) {
            return []
        }

        var selected = ({})
        for (var j = 0; j < configured.length; j++) {
            selected[configured[j]] = true
        }

        var result = []
        for (var k = 0; k < eligible.length; k++) {
            if (hasOwnKey(selected, String(eligible[k].provider))) {
                result.push(eligible[k])
                if (result.length >= maxOverviewProviders) {
                    break
                }
            }
        }
        return result
    }

    function configuredOverviewProviderIDs() {
        var raw = String(overviewProviderIDsRaw || "").trim()
        if (raw.length === 0 || raw === "__none__") {
            return []
        }
        var parts = raw.split(",")
        var result = []
        var seen = ({})
        for (var i = 0; i < parts.length; i++) {
            var trimmed = String(parts[i] || "").trim()
            if (trimmed.length === 0) {
                continue
            }
            // The settings page stores raw CLI provider IDs (e.g. groqcloud,
            // alibaba-coding-plan); normalize them to match the providerKey
            // form used for eligible[k].provider at runtime.
            var id = normalizedProviderID(trimmed)
            if (id.length === 0 || hasOwnKey(seen, id)) {
                continue
            }
            seen[id] = true
            result.push(id)
            if (result.length >= maxOverviewProviders) {
                break
            }
        }
        return result
    }

    function providerIndex(item) {
        return item ? providerIndexForID(item.provider) : -1
    }

    function providerIndexForID(providerID) {
        var id = String(providerID || "")
        if (id.length === 0 || !providers) {
            return -1
        }
        for (var i = 0; i < providers.length; i++) {
            if (providers[i] && providers[i].provider === id) {
                return i
            }
        }
        return -1
    }

    function overviewDetailText(item) {
        if (!item) {
            return ""
        }
        if (item.account && item.account.length > 0) {
            return item.account
        }
        if (item.status && item.status.length > 0) {
            return item.status
        }
        var placeholder = providerPlaceholderText(item)
        if (placeholder.length > 0) {
            return placeholder
        }
        if (item.source && item.source.length > 0) {
            return item.source
        }
        return ""
    }

    function providerPlaceholderText(item) {
        if (!item || !item.placeholder || item.placeholder.length === 0) {
            return ""
        }
        if (item.provider === "codex" && item.tokenCost) {
            return ""
        }
        return item.placeholder
    }

    function displayPercent(row) {
        if (!row || !row.hasPercent) {
            return 0
        }
        return usageBarsShowUsed ? row.usedPercent : row.leftPercent
    }

    function paceMarkerPercent(row) {
        if (!row || row.pacePercent < 0) {
            return -1
        }
        return usageBarsShowUsed ? row.pacePercent : clamp(100 - row.pacePercent, 0, 100)
    }

    function percentSuffix() {
        return usageBarsShowUsed ? i18n("used") : i18n("left")
    }

    function resetLabel(value) {
        var text = String(value || "").trim()
        if (text.length === 0) {
            return ""
        }
        // Only split where a unit letter runs into the next number
        // ("Resets5h30m" -> "Resets 5h 30m"). Splitting digit-then-letter as
        // well would also tear a number away from its own unit and render our
        // own compact durations ("2h 30m") as "2 h 30 m".
        text = text
            .replace(/([A-Za-z])(\d)/g, "$1 $2")
            .replace(/\)([A-Za-z])/g, ") $1")
            .replace(/(am|pm)\(/ig, "$1 (")
            .replace(/\s+/g, " ")
        if (/^resets\b/i.test(text)) {
            var rest = text.replace(/^resets\s*/i, "")
            return resetLabelLooksLikeTime(rest) ? i18n("Resets %1", rest) : rest
        }
        return resetLabelLooksLikeTime(text) ? i18n("Resets %1", text) : text
    }

    function resetLabelLooksLikeTime(value) {
        var text = String(value || "").trim()
        if (text.length === 0) {
            return false
        }
        if (/^(now|today|tomorrow)\b/i.test(text)) {
            return true
        }
        if (/^\d{1,2}(:\d{2})?\s*(am|pm)(\s*\([^)]+\))?$/i.test(text)) {
            return true
        }
        if (/^\d{1,2}:\d{2}(\s*\([^)]+\))?$/.test(text)) {
            return true
        }
        if (/^\S+\s+\d{1,2}:\d{2}(\s*\([^)]+\))?$/.test(text)) {
            return true
        }
        return /^\d+\s*(min|m|h|hr|hour|hours|d|day|days)(\s+\d+\s*(min|m|h|hr|hour|hours|d|day|days))*$/i.test(text)
    }

    function providerCountText(count) {
        var total = Math.max(0, Math.round(Number(count) || 0))
        return i18np("%1 provider", "%1 providers", total)
    }

    function clamp(value, minimum, maximum) {
        return Math.max(minimum, Math.min(maximum, value))
    }

    function primaryProvider() {
        return providers.length > 0 ? providers[0] : null
    }

    function selectedCompactProvider() {
        if (autoSelectProvider && selectedProviderData) {
            return selectedProviderData
        }
        return primaryProvider()
    }

    function updateSelectedProvider() {
        if (!providers || providers.length === 0) {
            return
        }

        if (autoSelectProvider) {
            // Don't override an Overview selection the user explicitly chose;
            // auto-select only drives the initial pick and provider tabs.
            if (selectionInitialized && overviewSelected) {
                return
            }
            selectedProviderID = providers[autoSelectedProviderIndex()].provider
            selectionInitialized = true
            return
        }

        if (!selectionInitialized) {
            selectedProviderID = overviewAvailable ? "" : providers[0].provider
            selectionInitialized = true
            return
        }
        if (selectedProviderIndex < 0
                && (!overviewAvailable || selectedProviderID.length > 0)) {
            selectedProviderID = providers[0].provider
        }
    }

    function autoSelectedProviderIndex() {
        var bestIndex = 0
        var bestScore = -1
        for (var i = 0; i < providers.length; i++) {
            var score = autoSelectScore(providers[i])
            if (score > bestScore) {
                bestScore = score
                bestIndex = i
            }
        }
        return bestIndex
    }

    function autoSelectScore(item) {
        if (!item || isOverviewErrorOnly(item)) {
            return -1
        }
        var percent = autoSelectUsedPercent(item)
        var incidentTieBreaker = notificationRank(item.statusSeverity) / 100
        return percent >= 0 ? percent + incidentTieBreaker : incidentTieBreaker
    }

    function autoSelectUsedPercent(item) {
        if (!item) {
            return -1
        }

        var best = -1
        var rows = item.rows || []
        for (var i = 0; i < rows.length; i++) {
            if (rows[i] && rows[i].hasPercent) {
                var used = Number(rows[i].usedPercent)
                if (isFinite(used)) {
                    best = Math.max(best, clamp(used, 0, 100))
                }
            }
        }
        if (item.providerCost && item.providerCost.percentUsed >= 0) {
            var providerCostUsed = Number(item.providerCost.percentUsed)
            if (isFinite(providerCostUsed)) {
                best = Math.max(best, clamp(providerCostUsed, 0, 100))
            }
        }
        return best
    }

    function compactProviders() {
        if (!providers || providers.length <= 1
                || Plasmoid.configuration.showMultiProviderInPanel !== true) {
            return []
        }

        var result = []
        for (var i = 0; i < providers.length && result.length < 4; i++) {
            if (switcherPercent(providers[i]) >= 0) {
                result.push(providers[i])
            }
        }
        return result
    }

    // Shared by the single-provider compact text and every multi-provider
    // panel entry so all panel display toggles behave identically.
    function compactProviderText(item) {
        if (!item) {
            return ""
        }

        var parts = []
        if (Plasmoid.configuration.showProviderInPanel) {
            parts.push(item.title)
        }

        var display = menuBarDisplayText(item)
        if (Plasmoid.configuration.showPercentInPanel && display.length > 0) {
            parts.push(display)
        }

        if (Plasmoid.configuration.showCreditsInPanel && item.credits !== null) {
            parts.push(i18n("%1cr", formatNumber(item.credits)))
        }

        return parts.join(" ")
    }

    function compactText() {
        var item = selectedCompactProvider()
        if (!item) {
            return loading ? i18n("Loading") : "CodexBar"
        }
        return compactProviderText(item)
    }

    function panelToolTipText() {
        var lines = []
        for (var i = 0; i < providers.length && lines.length < 6; i++) {
            var item = providers[i]
            if (!item) {
                continue
            }
            // Vertical panels collapse to a bare icon, so the tooltip is the only
            // incident surface there; never drop status just because usage exists.
            var incident = item.hasIncident && item.status.length > 0 ? item.status : ""
            var percent = switcherPercent(item)
            var line = ""
            if (percent >= 0) {
                line = i18n("%1: %2% %3", item.title, Math.round(percent), percentSuffix())
                if (incident.length > 0) {
                    line = i18n("%1 - %2", line, incident)
                }
            } else if (incident.length > 0) {
                line = i18n("%1: %2", item.title, incident)
            }
            if (line.length > 0) {
                lines.push(line)
            }
        }
        if (loading) {
            lines.push(i18n("Refreshing usage..."))
        }
        if (lines.length === 0 && errorText.length > 0) {
            return boundedDisplayText(errorText, 500)
        }
        return lines.join("\n")
    }

    function menuBarDisplayText(item) {
        if (!item) {
            return ""
        }

        var mode = String(menuBarDisplayMode || "percent")
        if (mode === "pace") {
            return primaryPaceText(item)
        }
        if (mode === "both") {
            var percentText = primaryPercentText(item)
            var paceText = primaryPaceText(item)
            if (percentText.length > 0 && paceText.length > 0) {
                return i18n("%1 - %2", percentText, paceText)
            }
            return percentText.length > 0 ? percentText : paceText
        }
        if (mode === "resetTime") {
            return primaryResetText(item)
        }
        return primaryPercentText(item)
    }

    function primaryPercentText(item) {
        var percent = switcherPercent(item)
        return percent >= 0 ? i18n("%1%", Math.round(percent)) : ""
    }

    function primaryPaceText(item) {
        var row = switcherMetricRow(item)
        if (!row || row.pacePercent < 0) {
            return ""
        }
        var shownPace = paceMarkerPercent(row)
        if (shownPace < 0) {
            return ""
        }
        return row.paceOnTop
            ? i18n("%1% pace", Math.round(shownPace))
            : i18n("%1% pace late", Math.round(shownPace))
    }

    function primaryResetText(item) {
        var row = switcherMetricRow(item)
        var reset = usageResetText(row)
        if (reset.length === 0) {
            return ""
        }
        return resetLabel(reset)
    }

    function formatNumber(value) {
        if (Math.abs(value) >= 100) {
            return Math.round(value).toString()
        }
        return Number(value).toFixed(1)
    }

    Plasma5Support.DataSource {
        id: usageSource

        engine: "executable"
        interval: 0

        onNewData: function(sourceName, data) {
            var rawStdoutText = data && data["stdout"] ? data["stdout"] : ""
            var stdoutText = SafeText.cliJsonText(rawStdoutText)
            var stderrText = data && data["stderr"] ? data["stderr"] : ""
            if (stdoutText === null) {
                stdoutText = ""
                stderrText = i18n("codexbar response exceeded the supported size.")
            }

            if (sourceName === root.connectedCostCommandSource) {
                root.connectedCostCommandSource = ""
                root.finishUsageCommandSource(sourceName)
                root.parseCostOutput(stdoutText, stderrText)
                return
            }

            if (sourceName === root.connectedProviderConfigCommandSource) {
                root.connectedProviderConfigCommandSource = ""
                root.finishUsageCommandSource(sourceName)
                root.parseProviderConfigOutput(stdoutText, stderrText)
                return
            }

            if (root.pendingAccountCommands[sourceName]) {
                root.parseProviderAccountsOutput(sourceName, stdoutText, stderrText)
                return
            }

            if (root.pendingProviderCommands[sourceName]) {
                root.parseProviderFallbackOutput(sourceName, stdoutText, stderrText)
                return
            }

            if (sourceName === root.connectedCommandSource) {
                root.connectedCommandSource = ""
                root.finishUsageCommandSource(sourceName)
                root.parseOutput(stdoutText, stderrText)
                return
            }

            if (root.activeUsageCommands[sourceName]) {
                root.finishUsageCommandSource(sourceName)
            }
        }
    }

    Timer {
        id: usageRefreshTimer

        interval: Math.max(1, root.refreshIntervalSec) * 1000
        repeat: true
        running: root.refreshIntervalSec > 0
        triggeredOnStart: false
        onTriggered: {
            if (!root.hasPendingUsageCommandTimeouts()) {
                root.refreshNow()
            }
        }
    }

    Timer {
        id: usageCommandTimeoutTimer

        interval: 1000
        repeat: true
        running: root.hasPendingUsageCommandTimeouts()
        triggeredOnStart: false
        onTriggered: root.expireUsageCommands(Date.now())
    }

    Timer {
        id: accountCommandTimeoutTimer

        interval: 1000
        repeat: true
        running: root.hasPendingAccountCommands()
        triggeredOnStart: false
        onTriggered: root.expirePendingAccountCommands(Date.now())
    }

    Plasma5Support.DataSource {
        id: providerConfigWatcher

        engine: "executable"
        interval: root.providerConfigWatchIntervalMs

        onNewData: function(sourceName, data) {
            if (sourceName !== root.providerConfigWatchCommand) {
                return
            }
            var stdoutText = data && data["stdout"] ? data["stdout"] : ""
            root.handleProviderConfigWatch(stdoutText)
        }
    }

    Timer {
        id: updateCheckTimer

        repeat: false
        running: false
        triggeredOnStart: false
        onTriggered: root.checkForWidgetUpdate()
    }

    Timer {
        id: updateCommandTimeoutTimer

        repeat: false
        onTriggered: root.handleUpdateCommandTimeout()
    }

    Plasma5Support.DataSource {
        id: updateSource

        engine: "executable"

        onNewData: function(sourceName, data) {
            var rawStdoutText = data && data["stdout"] ? data["stdout"] : ""
            var stdoutText = SafeText.cliJsonText(rawStdoutText)
            var stderrText = data && data["stderr"] ? data["stderr"] : ""
            if (stdoutText === null) {
                stdoutText = ""
                stderrText = i18n("Widget updater response exceeded the supported size.")
            }
            root.handleUpdateData(sourceName, stdoutText, stderrText)
        }
    }

    Plasma5Support.DataSource {
        id: notificationSource

        engine: "executable"

        onNewData: function(sourceName, data) {
            notificationSource.disconnectSource(sourceName)
        }
    }

    compactRepresentation: Components.CompactRepresentation {
        applet: root
    }

    fullRepresentation: Item {
        id: fullRoot

        implicitWidth: Kirigami.Units.gridUnit * 34
        implicitHeight: Kirigami.Units.gridUnit * 38
        Layout.minimumWidth: Kirigami.Units.gridUnit * 30
        Layout.minimumHeight: Kirigami.Units.gridUnit * 28
        Layout.preferredWidth: implicitWidth
        Layout.preferredHeight: implicitHeight

        Rectangle {
            id: popupInnerSurface

            anchors.fill: parent
            radius: root.roundedSurfaceRadius
            color: root.withAlpha(Kirigami.Theme.alternateBackgroundColor, 0.18)
            border.width: 1
            border.color: root.withAlpha(Kirigami.Theme.textColor, 0.09)

            Rectangle {
                anchors.fill: parent
                anchors.margins: 1
                radius: Math.max(0, parent.radius - 1)
                color: "transparent"
                border.width: 1
                border.color: root.withAlpha(Kirigami.Theme.backgroundColor, 0.28)
            }
        }

        ColumnLayout {
            anchors.fill: parent
            anchors.margins: Kirigami.Units.largeSpacing
            spacing: Kirigami.Units.largeSpacing

            Item {
                id: providerTabsBar

                visible: providers.length > 0
                Layout.fillWidth: true
                Layout.preferredHeight: Kirigami.Units.gridUnit * 2.35

                Rectangle {
                    id: providerTabsSurface

                    anchors.fill: parent
                    radius: root.roundedSurfaceRadius
                    color: root.withAlpha(Kirigami.Theme.textColor, 0.035)
                    border.width: 1
                    border.color: root.withAlpha(Kirigami.Theme.textColor, 0.06)
                }

                Flickable {
                    id: providerTabsFlickable

                    anchors.fill: parent
                    anchors.margins: Kirigami.Units.smallSpacing / 2
                    clip: true
                    boundsBehavior: Flickable.StopAtBounds
                    contentWidth: providerTabs.implicitWidth
                    contentHeight: height
                    interactive: contentWidth > width

                    RowLayout {
                        id: providerTabs

                        height: providerTabsFlickable.height
                        spacing: Kirigami.Units.smallSpacing / 2

                        Rectangle {
                            id: overviewTab

                            property bool focusAcquiredByPointer: false
                            readonly property bool selected: root.overviewSelected
                            readonly property bool keyboardFocusVisible: activeFocus && !focusAcquiredByPointer
                            readonly property color brandAccent: Kirigami.Theme.highlightColor
                            readonly property color accent: root.readableAccentColor(
                                brandAccent,
                                Kirigami.Theme.backgroundColor)
                            readonly property color foreground: selected
                                ? Kirigami.Theme.textColor
                                : root.withAlpha(Kirigami.Theme.textColor, 0.72)

                            function activate() {
                                root.selectedProviderID = ""
                                root.selectionInitialized = true
                            }

                            visible: root.overviewAvailable
                            Layout.preferredWidth: Math.max(
                                Kirigami.Units.gridUnit * 5.2,
                                overviewTabLabel.implicitWidth + Kirigami.Units.gridUnit * 2.2)
                            Layout.preferredHeight: providerTabsFlickable.height
                            radius: root.roundedSurfaceRadius
                            color: overviewTabMouse.pressed
                                ? root.withAlpha(Kirigami.Theme.focusColor, 0.1)
                                : (selected
                                ? root.withAlpha(Kirigami.Theme.textColor, 0.045)
                                : (keyboardFocusVisible
                                ? root.withAlpha(Kirigami.Theme.focusColor, 0.06)
                                : (overviewTabMouse.containsMouse ? root.withAlpha(Kirigami.Theme.textColor, 0.05) : "transparent")
                                ))
                            border.width: keyboardFocusVisible ? 1 : 0
                            border.color: Kirigami.Theme.focusColor
                            scale: overviewTabMouse.pressed ? 0.985 : 1
                            activeFocusOnTab: true

                            onActiveFocusChanged: {
                                if (!activeFocus) {
                                    focusAcquiredByPointer = false
                                }
                            }

                            Accessible.role: Accessible.PageTab
                            Accessible.name: i18n("Overview")
                            Accessible.selectable: true
                            Accessible.selected: selected
                            Accessible.onPressAction: overviewTab.activate()

                            Keys.onPressed: function(event) {
                                overviewTab.focusAcquiredByPointer = false
                                switch (event.key) {
                                case Qt.Key_Space:
                                case Qt.Key_Enter:
                                case Qt.Key_Return:
                                case Qt.Key_Select:
                                    overviewTab.activate()
                                    event.accepted = true
                                    break
                                }
                            }

                            Behavior on color {
                                ColorAnimation {
                                    duration: Kirigami.Units.shortDuration
                                }
                            }

                            Behavior on scale {
                                NumberAnimation {
                                    duration: Kirigami.Units.shortDuration
                                    easing.type: Easing.OutCubic
                                }
                            }

                            MouseArea {
                                id: overviewTabMouse

                                anchors.fill: parent
                                hoverEnabled: true
                                cursorShape: Qt.PointingHandCursor
                                onPressed: {
                                    overviewTab.focusAcquiredByPointer = true
                                    overviewTab.forceActiveFocus(Qt.MouseFocusReason)
                                }
                                onClicked: overviewTab.activate()
                            }

                            RowLayout {
                                anchors.fill: parent
                                anchors.margins: Kirigami.Units.smallSpacing
                                anchors.bottomMargin: Kirigami.Units.smallSpacing + 2
                                spacing: Kirigami.Units.smallSpacing

                                Kirigami.Icon {
                                    source: "view-grid-symbolic"
                                    isMask: true
                                    color: overviewTab.selected ? overviewTab.accent : overviewTab.foreground
                                    Layout.preferredWidth: Kirigami.Units.iconSizes.small
                                    Layout.preferredHeight: Kirigami.Units.iconSizes.small
                                }

                                PlasmaComponents.Label {
                                    id: overviewTabLabel

                                    text: i18n("Overview")
                                    font.weight: overviewTab.selected ? Font.DemiBold : Font.Normal
                                    color: overviewTab.foreground
                                    elide: Text.ElideRight
                                    Layout.fillWidth: true
                                }
                            }

                            Rectangle {
                                anchors.left: parent.left
                                anchors.right: parent.right
                                anchors.bottom: parent.bottom
                                anchors.leftMargin: Kirigami.Units.smallSpacing
                                anchors.rightMargin: Kirigami.Units.smallSpacing
                                anchors.bottomMargin: 2
                                height: 2
                                radius: height / 2
                                color: overviewTab.selected ? overviewTab.accent : "transparent"
                            }
                        }

                        Repeater {
                            model: providers

                            delegate: Rectangle {
                                id: providerTab

                                property bool focusAcquiredByPointer: false
                                readonly property bool selected: index === root.selectedProviderIndex
                                readonly property bool keyboardFocusVisible: activeFocus && !focusAcquiredByPointer
                                readonly property real meter: root.switcherPercent(modelData)
                                readonly property color accent: root.providerReadableColor(
                                    modelData.provider,
                                    Kirigami.Theme.backgroundColor)
                                readonly property color foreground: selected
                                    ? Kirigami.Theme.textColor
                                    : root.withAlpha(Kirigami.Theme.textColor, 0.72)

                                function activate() {
                                    root.selectedProviderID = modelData.provider
                                    root.selectionInitialized = true
                                }

                                Layout.preferredWidth: Math.min(
                                    Kirigami.Units.gridUnit * 7,
                                    Math.max(Kirigami.Units.gridUnit * 4.2,
                                        providerTabLabel.implicitWidth + Kirigami.Units.gridUnit * 2.2))
                                Layout.preferredHeight: providerTabsFlickable.height
                                radius: root.roundedSurfaceRadius
                                color: providerTabMouse.pressed
                                    ? root.withAlpha(Kirigami.Theme.focusColor, 0.1)
                                    : (selected
                                    ? root.withAlpha(Kirigami.Theme.textColor, 0.045)
                                    : (keyboardFocusVisible
                                    ? root.withAlpha(Kirigami.Theme.focusColor, 0.06)
                                    : (providerTabMouse.containsMouse ? root.withAlpha(Kirigami.Theme.textColor, 0.05) : "transparent")
                                    ))
                                border.width: keyboardFocusVisible ? 1 : 0
                                border.color: Kirigami.Theme.focusColor
                                opacity: modelData.error.length > 0 ? 0.62 : 1
                                scale: providerTabMouse.pressed ? 0.985 : 1
                                activeFocusOnTab: true

                                onActiveFocusChanged: {
                                    if (!activeFocus) {
                                        focusAcquiredByPointer = false
                                    }
                                }

                                Accessible.role: Accessible.PageTab
                                Accessible.name: modelData.title
                                Accessible.selectable: true
                                Accessible.selected: selected
                                Accessible.onPressAction: providerTab.activate()

                                Keys.onPressed: function(event) {
                                    providerTab.focusAcquiredByPointer = false
                                    switch (event.key) {
                                    case Qt.Key_Space:
                                    case Qt.Key_Enter:
                                    case Qt.Key_Return:
                                    case Qt.Key_Select:
                                        providerTab.activate()
                                        event.accepted = true
                                        break
                                    }
                                }

                                Behavior on color {
                                    ColorAnimation {
                                        duration: Kirigami.Units.shortDuration
                                    }
                                }

                                Behavior on scale {
                                    NumberAnimation {
                                        duration: Kirigami.Units.shortDuration
                                        easing.type: Easing.OutCubic
                                    }
                                }

                                MouseArea {
                                    id: providerTabMouse

                                    anchors.fill: parent
                                    hoverEnabled: true
                                    cursorShape: Qt.PointingHandCursor
                                    onPressed: {
                                        providerTab.focusAcquiredByPointer = true
                                        providerTab.forceActiveFocus(Qt.MouseFocusReason)
                                    }
                                    onClicked: providerTab.activate()
                                }

                                RowLayout {
                                    anchors.fill: parent
                                    anchors.margins: Kirigami.Units.smallSpacing
                                    anchors.bottomMargin: Kirigami.Units.smallSpacing + 2
                                    spacing: Kirigami.Units.smallSpacing

                                    Kirigami.Icon {
                                        source: root.providerIconSource(modelData.provider)
                                        fallback: "view-statistics"
                                        isMask: root.providerIconIsMask(modelData.provider)
                                        color: providerTab.accent
                                        Layout.preferredWidth: Kirigami.Units.iconSizes.small
                                        Layout.preferredHeight: Kirigami.Units.iconSizes.small
                                    }

                                    PlasmaComponents.Label {
                                        id: providerTabLabel

                                        text: modelData.title
                                        font.weight: providerTab.selected ? Font.DemiBold : Font.Normal
                                        color: providerTab.foreground
                                        elide: Text.ElideRight
                                        Layout.fillWidth: true
                                    }
                                }

                                Rectangle {
                                    anchors.left: parent.left
                                    anchors.right: parent.right
                                    anchors.bottom: parent.bottom
                                    anchors.leftMargin: Kirigami.Units.smallSpacing
                                    anchors.rightMargin: Kirigami.Units.smallSpacing
                                    anchors.bottomMargin: 2
                                    height: 2
                                    radius: height / 2
                                    color: root.withAlpha(Kirigami.Theme.textColor, 0.12)
                                    clip: true

                                    Rectangle {
                                        visible: providerTab.meter >= 0
                                        width: providerTab.meter <= 0
                                            ? 0
                                            : Math.max(parent.height, parent.width * Math.max(0, Math.min(100, providerTab.meter)) / 100)
                                        height: parent.height
                                        radius: parent.radius
                                        color: providerTab.accent

                                        Behavior on width {
                                            NumberAnimation {
                                                duration: Kirigami.Units.longDuration
                                                easing.type: Easing.OutCubic
                                            }
                                        }
                                    }
                                }
                            }
                        }
                    }
                }

                Rectangle {
                    id: providerTabsLeftFade

                    anchors.left: parent.left
                    anchors.top: parent.top
                    anchors.bottom: parent.bottom
                    width: Kirigami.Units.gridUnit
                    visible: opacity > 0
                    opacity: providerTabsFlickable.interactive && providerTabsFlickable.contentX > 0 ? 1 : 0
                    gradient: Gradient {
                        orientation: Gradient.Horizontal

                        GradientStop { position: 0; color: Kirigami.Theme.backgroundColor }
                        GradientStop { position: 1; color: root.withAlpha(Kirigami.Theme.backgroundColor, 0) }
                    }

                    Behavior on opacity {
                        NumberAnimation {
                            duration: Kirigami.Units.shortDuration
                        }
                    }
                }

                Rectangle {
                    id: providerTabsRightFade

                    anchors.right: parent.right
                    anchors.top: parent.top
                    anchors.bottom: parent.bottom
                    width: Kirigami.Units.gridUnit
                    visible: opacity > 0
                    opacity: providerTabsFlickable.interactive
                        && providerTabsFlickable.contentX < providerTabsFlickable.contentWidth - providerTabsFlickable.width - 1 ? 1 : 0
                    gradient: Gradient {
                        orientation: Gradient.Horizontal

                        GradientStop { position: 0; color: root.withAlpha(Kirigami.Theme.backgroundColor, 0) }
                        GradientStop { position: 1; color: Kirigami.Theme.backgroundColor }
                    }

                    Behavior on opacity {
                        NumberAnimation {
                            duration: Kirigami.Units.shortDuration
                        }
                    }
                }
            }

            Kirigami.Separator {
                visible: providers.length > 0
                Layout.fillWidth: true
            }

            Kirigami.InlineMessage {
                id: globalErrorMessage

                visible: errorText.length > 0
                text: errorText
                type: Kirigami.MessageType.Error
                Layout.fillWidth: true
            }

            RowLayout {
                visible: providers.length === 0 && errorText.length === 0 && loading
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: Kirigami.Units.smallSpacing

                Item {
                    Layout.fillWidth: true
                }

                Controls.BusyIndicator {
                    running: parent.visible
                    Layout.preferredWidth: Kirigami.Units.iconSizes.medium
                    Layout.preferredHeight: Kirigami.Units.iconSizes.medium
                }

                PlasmaComponents.Label {
                    text: i18n("Loading usage...")
                    opacity: root.secondaryTextOpacity
                }

                Item {
                    Layout.fillWidth: true
                }
            }

            Kirigami.PlaceholderMessage {
                id: emptyProvidersMessage

                visible: providers.length === 0 && errorText.length === 0 && !loading
                text: i18n("No provider data.")
                icon.name: "view-statistics-symbolic"
                type: Kirigami.PlaceholderMessage.Type.Informational
                Layout.fillWidth: true
                Layout.fillHeight: true
            }

            ColumnLayout {
                visible: root.overviewSelected
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: Kirigami.Units.largeSpacing

                RowLayout {
                    id: overviewHeaderRow

                    Layout.fillWidth: true
                    Layout.rightMargin: Kirigami.Units.smallSpacing
                    spacing: Kirigami.Units.smallSpacing

                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: Kirigami.Units.smallSpacing / 2

                        Kirigami.Heading {
                            text: i18n("Overview")
                            level: 2
                            type: Kirigami.Heading.Type.Primary
                            Layout.fillWidth: true
                            elide: Text.ElideRight
                        }

                        PlasmaComponents.Label {
                            readonly property int providerCount: root.overviewProviderItems.length

                            text: lastUpdatedText.length > 0
                                ? i18n("%1 - %2", lastUpdatedText, root.providerCountText(providerCount))
                                : root.providerCountText(providerCount)
                            opacity: root.secondaryTextOpacity
                            Layout.fillWidth: true
                            elide: Text.ElideRight
                        }
                    }

                    PlasmaComponents.ToolButton {
                        icon.name: "view-refresh"
                        enabled: !loading
                        Accessible.name: i18n("Refresh")
                        onClicked: root.refreshNow()
                    }
                }

                PlasmaComponents.ScrollView {
                    id: overviewScroll

                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    contentWidth: availableWidth
                    clip: true
                    PlasmaComponents.ScrollBar.horizontal.policy: PlasmaComponents.ScrollBar.AlwaysOff

                    ColumnLayout {
                        width: Math.max(
                            0,
                            overviewScroll.availableWidth - Kirigami.Units.smallSpacing)
                        spacing: Kirigami.Units.smallSpacing

                        Kirigami.PlaceholderMessage {
                            id: overviewPlaceholderMessage

                            visible: root.overviewProviderItems.length === 0
                            text: i18n("No overview data available.")
                            icon.name: "view-grid-symbolic"
                            type: Kirigami.PlaceholderMessage.Type.Informational
                            Layout.fillWidth: true
                            Layout.preferredHeight: Kirigami.Units.gridUnit * 8
                        }

                        Repeater {
                            model: root.overviewProviderItems

                            delegate: Components.OverviewProviderRow {
                                applet: root
                                onSelected: function(providerData) {
                                    var nextProviderIndex = root.providerIndex(providerData)
                                    if (nextProviderIndex >= 0) {
                                        root.selectedProviderID = providerData.provider
                                        root.selectionInitialized = true
                                    }
                                }
                            }
                        }
                    }
                }
            }

            ColumnLayout {
                visible: root.selectedProviderData !== null
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: Kirigami.Units.largeSpacing

                Components.ProviderHeader {
                    applet: root
                    providerData: root.selectedProviderData
                }

                Components.ProviderAccountsPanel {
                    applet: root
                    providerData: root.selectedProviderData
                }

                Kirigami.InlineMessage {
                    id: providerStatusMessage

                    visible: root.selectedProviderData
                        && root.selectedProviderData.hasIncident
                        && root.selectedProviderData.status
                        && root.selectedProviderData.status.length > 0
                    text: root.selectedProviderData ? root.selectedProviderData.status : ""
                    type: root.selectedProviderData
                        ? root.statusMessageType(root.selectedProviderData.statusSeverity)
                        : Kirigami.MessageType.Information
                    Layout.fillWidth: true
                }

                Kirigami.InlineMessage {
                    id: providerErrorMessage

                    visible: root.selectedProviderData
                        && root.selectedProviderData.error
                        && root.selectedProviderData.error.length > 0
                    text: root.selectedProviderData ? root.selectedProviderData.error : ""
                    type: Kirigami.MessageType.Error
                    Layout.fillWidth: true
                }

                PlasmaComponents.ScrollView {
                    id: providerScroll

                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    contentWidth: availableWidth
                    clip: true
                    PlasmaComponents.ScrollBar.horizontal.policy: PlasmaComponents.ScrollBar.AlwaysOff

                    ColumnLayout {
                        width: Math.max(
                            0,
                            providerScroll.availableWidth - Kirigami.Units.smallSpacing)
                        spacing: Kirigami.Units.largeSpacing

                        Kirigami.PlaceholderMessage {
                            id: providerPlaceholderMessage

                            visible: root.providerPlaceholderText(root.selectedProviderData).length > 0
                            text: root.providerPlaceholderText(root.selectedProviderData)
                            icon.name: "view-statistics-symbolic"
                            type: Kirigami.PlaceholderMessage.Type.Informational
                            Layout.fillWidth: true
                            Layout.preferredHeight: Kirigami.Units.gridUnit * 8
                        }

                        Repeater {
                            model: root.selectedProviderData ? root.selectedProviderData.rows : []

                            delegate: Components.ProviderUsageRow {
                                applet: root
                                providerData: root.selectedProviderData
                            }
                        }

                        Kirigami.Separator {
                            visible: root.hasAdditionalSections(root.selectedProviderData)
                            Layout.fillWidth: true
                        }

                        ColumnLayout {
                            visible: root.selectedProviderData && root.selectedProviderData.credits !== null
                            Layout.fillWidth: true
                            spacing: Kirigami.Units.smallSpacing / 1.5

                            Kirigami.Heading {
                                text: i18n("Credits")
                                level: 4
                                type: Kirigami.Heading.Type.Primary
                                Layout.fillWidth: true
                            }

                            Rectangle {
                                // A depleted balance draws an empty track that is
                                // indistinguishable from a meter that has not loaded
                                // yet, so let the remaining-credits line carry the
                                // zero case on its own.
                                visible: root.selectedProviderData && root.selectedProviderData.credits > 0
                                Layout.fillWidth: true
                                Layout.preferredHeight: root.meterTrackHeight
                                radius: height / 2
                                color: root.withAlpha(Kirigami.Theme.textColor, 0.1)
                                clip: true

                                Rectangle {
                                    width: root.selectedProviderData && root.selectedProviderData.credits > 0
                                        ? Math.max(parent.height, parent.width * Math.min(root.selectedProviderData.credits, 1000) / 1000)
                                        : 0
                                    height: parent.height
                                    radius: parent.radius
                                    color: root.providerReadableColor(root.selectedProviderData ? root.selectedProviderData.provider : "")

                                    Behavior on width {
                                        NumberAnimation {
                                            duration: Kirigami.Units.longDuration
                                            easing.type: Easing.OutCubic
                                        }
                                    }
                                }
                            }

                            RowLayout {
                                Layout.fillWidth: true
                                spacing: Kirigami.Units.smallSpacing

                                PlasmaComponents.Label {
                                    text: i18n("Remaining: %1", root.selectedProviderData ? root.formatNumber(root.selectedProviderData.credits) : "")
                                    Layout.fillWidth: true
                                    elide: Text.ElideRight
                                }
                            }
                        }

                        ColumnLayout {
                            id: resetCreditsSection

                            readonly property var resetCredits: root.selectedProviderData ? root.selectedProviderData.resetCredits : null

                            visible: resetCreditsSection.resetCredits ? true : false
                            Layout.fillWidth: true
                            spacing: Kirigami.Units.smallSpacing / 1.5

                            Kirigami.Separator {
                                Layout.fillWidth: true
                            }

                            Kirigami.Heading {
                                text: resetCreditsSection.resetCredits ? resetCreditsSection.resetCredits.title : ""
                                level: 4
                                type: Kirigami.Heading.Type.Primary
                                Layout.fillWidth: true
                            }

                            PlasmaComponents.Label {
                                text: resetCreditsSection.resetCredits ? resetCreditsSection.resetCredits.line : ""
                                opacity: root.secondaryTextOpacity
                                Layout.fillWidth: true
                                elide: Text.ElideRight
                            }
                        }

                        ColumnLayout {
                            id: providerCostSection

                            readonly property var providerCost: root.selectedProviderData ? root.selectedProviderData.providerCost : null
                            readonly property color accent: root.providerReadableColor(root.selectedProviderData ? root.selectedProviderData.provider : "")

                            visible: providerCostSection.providerCost ? true : false
                            Layout.fillWidth: true
                            spacing: Kirigami.Units.smallSpacing / 1.5

                            Kirigami.Separator {
                                Layout.fillWidth: true
                            }

                            Kirigami.Heading {
                                text: providerCostSection.providerCost ? providerCostSection.providerCost.title : ""
                                level: 4
                                type: Kirigami.Heading.Type.Primary
                                Layout.fillWidth: true
                            }

                            Rectangle {
                                visible: providerCostSection.providerCost && providerCostSection.providerCost.percentUsed >= 0 ? true : false
                                Layout.fillWidth: true
                                Layout.preferredHeight: root.meterTrackHeight
                                radius: height / 2
                                color: root.withAlpha(Kirigami.Theme.textColor, 0.1)
                                clip: true

                                Rectangle {
                                    width: providerCostSection.providerCost && providerCostSection.providerCost.percentUsed > 0
                                        ? Math.max(parent.height, parent.width * providerCostSection.providerCost.percentUsed / 100)
                                        : 0
                                    height: parent.height
                                    radius: parent.radius
                                    color: providerCostSection.accent

                                    Behavior on width {
                                        NumberAnimation {
                                            duration: Kirigami.Units.longDuration
                                            easing.type: Easing.OutCubic
                                        }
                                    }
                                }
                            }

                            RowLayout {
                                Layout.fillWidth: true
                                spacing: Kirigami.Units.smallSpacing

                                PlasmaComponents.Label {
                                    text: providerCostSection.providerCost ? providerCostSection.providerCost.spendLine : ""
                                    Layout.fillWidth: true
                                    elide: Text.ElideRight
                                }

                                PlasmaComponents.Label {
                                    visible: providerCostSection.providerCost && providerCostSection.providerCost.percentLine.length > 0 ? true : false
                                    text: providerCostSection.providerCost ? providerCostSection.providerCost.percentLine : ""
                                    opacity: root.secondaryTextOpacity
                                    horizontalAlignment: Text.AlignRight
                                    elide: Text.ElideRight
                                }
                            }

                            PlasmaComponents.Label {
                                visible: providerCostSection.providerCost && providerCostSection.providerCost.personalSpendLine.length > 0 ? true : false
                                text: providerCostSection.providerCost ? providerCostSection.providerCost.personalSpendLine : ""
                                opacity: root.secondaryTextOpacity
                                Layout.fillWidth: true
                                elide: Text.ElideRight
                            }
                        }

                        ColumnLayout {
                            id: providerDetailsSection

                            readonly property var details: root.selectedProviderData
                                ? root.selectedProviderData.providerDetails || []
                                : []

                            visible: details.length > 0
                            Layout.fillWidth: true
                            spacing: Kirigami.Units.smallSpacing

                            Kirigami.Separator {
                                Layout.fillWidth: true
                            }

                            Repeater {
                                model: providerDetailsSection.details

                                delegate: Components.ProviderDetailSection {
                                    applet: root
                                    providerData: root.selectedProviderData
                                }
                            }
                        }

                        ColumnLayout {
                            id: usageDashboardSection

                            readonly property var dashboard: root.selectedProviderData ? root.selectedProviderData.usageDashboard : null
                            readonly property var kpis: dashboard ? dashboard.kpis : []
                            readonly property var rows: dashboard ? dashboard.rows : []

                            visible: kpis.length > 0 || rows.length > 0
                            Layout.fillWidth: true
                            spacing: Kirigami.Units.smallSpacing / 1.5

                            Kirigami.Separator {
                                Layout.fillWidth: true
                            }

                            PlasmaComponents.Label {
                                text: i18n("Usage dashboard")
                                font.weight: Font.DemiBold
                                Layout.fillWidth: true
                                elide: Text.ElideRight
                            }

                            GridLayout {
                                visible: usageDashboardSection.kpis.length > 0
                                Layout.fillWidth: true
                                columns: 2
                                columnSpacing: Kirigami.Units.smallSpacing
                                rowSpacing: Kirigami.Units.smallSpacing / 2

                                Repeater {
                                    model: usageDashboardSection.kpis

                                    delegate: ColumnLayout {
                                        Layout.fillWidth: true
                                        spacing: 0

                                        PlasmaComponents.Label {
                                            text: modelData.label
                                            opacity: root.secondaryTextOpacity
                                            font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                            Layout.fillWidth: true
                                            elide: Text.ElideRight
                                        }

                                        PlasmaComponents.Label {
                                            text: modelData.value
                                            font.weight: Font.DemiBold
                                            Layout.fillWidth: true
                                            elide: Text.ElideRight
                                        }
                                    }
                                }
                            }

                            ColumnLayout {
                                visible: usageDashboardSection.rows.length > 0
                                Layout.fillWidth: true
                                spacing: Kirigami.Units.smallSpacing / 2

                                Repeater {
                                    model: usageDashboardSection.rows

                                    delegate: RowLayout {
                                        Layout.fillWidth: true
                                        spacing: Kirigami.Units.smallSpacing

                                        PlasmaComponents.Label {
                                            text: modelData.label
                                            opacity: root.secondaryTextOpacity
                                            Layout.fillWidth: true
                                            elide: Text.ElideRight
                                        }

                                        PlasmaComponents.Label {
                                            text: modelData.value
                                            opacity: root.valueTextOpacity
                                            horizontalAlignment: Text.AlignRight
                                            elide: Text.ElideRight
                                        }
                                    }
                                }
                            }
                        }

                        ColumnLayout {
                            id: tokenCostSection

                            readonly property var tokenCost: root.selectedProviderData ? root.selectedProviderData.tokenCost : null
                            readonly property string costErrorText: root.costErrorText
                            readonly property bool supportsLocalCost: root.selectedProviderData
                                && root.tokenCostHint(root.selectedProviderData.provider).length > 0

                            visible: tokenCostSection.tokenCost
                                ? true
                                : tokenCostSection.supportsLocalCost && tokenCostSection.costErrorText.length > 0
                            Layout.fillWidth: true
                            spacing: Kirigami.Units.smallSpacing / 1.5

                            Kirigami.Separator {
                                Layout.fillWidth: true
                            }

                            Kirigami.Heading {
                                text: i18n("Cost")
                                level: 4
                                type: Kirigami.Heading.Type.Primary
                                Layout.fillWidth: true
                            }

                            PlasmaComponents.Label {
                                visible: !tokenCostSection.tokenCost && tokenCostSection.costErrorText.length > 0
                                text: i18n("Cost unavailable: %1", tokenCostSection.costErrorText)
                                color: Kirigami.Theme.negativeTextColor
                                Layout.fillWidth: true
                                wrapMode: Text.WordWrap
                            }

                            PlasmaComponents.Label {
                                id: costSessionSummaryLabel

                                visible: tokenCostSection.tokenCost ? true : false
                                text: tokenCostSection.tokenCost ? tokenCostSection.tokenCost.sessionLine : ""
                                font.weight: Font.DemiBold
                                Layout.fillWidth: true
                                elide: Text.ElideRight
                            }

                            PlasmaComponents.Label {
                                id: costMonthSummaryLabel

                                visible: tokenCostSection.tokenCost ? true : false
                                text: tokenCostSection.tokenCost ? tokenCostSection.tokenCost.monthLine : ""
                                font: Kirigami.Theme.smallFont
                                opacity: root.secondaryTextOpacity
                                Layout.fillWidth: true
                                elide: Text.ElideRight
                            }

                            Canvas {
                                id: costSparkline

                                readonly property var tokenCost: tokenCostSection.tokenCost
                                readonly property var providerData: root.selectedProviderData

                                property var points: tokenCost ? tokenCost.daily : []
                                readonly property real maxValue: root.costSparklineMax(points)
                                readonly property color accent: root.providerReadableColor(providerData ? providerData.provider : "")

                                visible: points.length > 1 && maxValue > 0
                                Layout.fillWidth: true
                                Layout.preferredHeight: Kirigami.Units.gridUnit * 3.25
                                Layout.topMargin: Kirigami.Units.smallSpacing / 2

                                onPointsChanged: requestPaint()
                                onMaxValueChanged: requestPaint()
                                onWidthChanged: requestPaint()
                                onHeightChanged: requestPaint()
                                onVisibleChanged: if (visible) requestPaint()

                                onPaint: {
                                    var ctx = getContext("2d")
                                    ctx.clearRect(0, 0, width, height)
                                    if (!points || points.length < 2 || maxValue <= 0 || width <= 0 || height <= 0) {
                                        return
                                    }

                                    var geometry = root.chartBarGeometry(width, points.length)
                                    var baseline = height - 1

                                    ctx.fillStyle = root.canvasColor(Kirigami.Theme.textColor, 0.1)
                                    ctx.fillRect(0, baseline, width, 1)

                                    var peakFill = root.buildChartBarGradient(
                                        ctx, costSparkline.accent, baseline, 0.96, 0.58)
                                    var normalFill = root.buildChartBarGradient(
                                        ctx, costSparkline.accent, baseline, 0.7, 0.3)
                                    for (var i = 0; i < points.length; i++) {
                                        var value = Math.max(0, Number(points[i].cost) || 0)
                                        var barHeight = Math.max(2, (height - 3) * value / maxValue)
                                        ctx.fillStyle = value === maxValue ? peakFill : normalFill
                                        root.paintRoundedTopBar(
                                            ctx,
                                            i * geometry.step,
                                            baseline,
                                            geometry.barWidth,
                                            barHeight,
                                            Kirigami.Units.smallSpacing / 2)
                                    }
                                }
                            }

                            RowLayout {
                                visible: tokenCostSection.tokenCost
                                    && tokenCostSection.tokenCost.daily
                                    && tokenCostSection.tokenCost.daily.length > 1
                                Layout.fillWidth: true
                                spacing: Kirigami.Units.smallSpacing

                                PlasmaComponents.Label {
                                    id: costSparklineSummaryLabel

                                    text: tokenCostSection.tokenCost ? root.costSparklineSummary(tokenCostSection.tokenCost.daily) : ""
                                    font: Kirigami.Theme.smallFont
                                    opacity: root.secondaryTextOpacity
                                    Layout.fillWidth: true
                                    elide: Text.ElideRight
                                }

                                PlasmaComponents.Label {
                                    id: costSparklineRangeLabel

                                    text: tokenCostSection.tokenCost
                                        ? i18np("%1 day", "%1 days", tokenCostSection.tokenCost.daily.length)
                                        : ""
                                    font: Kirigami.Theme.smallFont
                                    opacity: root.secondaryTextOpacity
                                    horizontalAlignment: Text.AlignRight
                                    elide: Text.ElideRight
                                }
                            }

                            ColumnLayout {
                                id: costHistoryChartSection

                                readonly property var rows: root.costHistoryRows(tokenCostSection.tokenCost)
                                readonly property string peakLine: tokenCostSection.tokenCost ? root.costPeakLine(tokenCostSection.tokenCost.daily) : ""
                                readonly property string averageLine: tokenCostSection.tokenCost ? root.costAverageDailyLine(tokenCostSection.tokenCost.daily) : ""
                                readonly property color accent: root.providerReadableColor(root.selectedProviderData ? root.selectedProviderData.provider : "")

                                visible: rows.length > 1
                                Layout.fillWidth: true
                                spacing: Kirigami.Units.smallSpacing / 2

                                RowLayout {
                                    id: costHistoryHeaderRow

                                    Layout.fillWidth: true
                                    spacing: Kirigami.Units.smallSpacing

                                    PlasmaComponents.Label {
                                        text: i18n("Cost history")
                                        font.weight: Font.DemiBold
                                        Layout.fillWidth: true
                                        elide: Text.ElideRight
                                    }

                                    PlasmaComponents.Label {
                                        visible: costHistoryChartSection.averageLine.length > 0
                                        text: costHistoryChartSection.averageLine
                                        font: Kirigami.Theme.smallFont
                                        opacity: root.secondaryTextOpacity
                                        horizontalAlignment: Text.AlignRight
                                        elide: Text.ElideRight
                                    }
                                }

                                PlasmaComponents.Label {
                                    visible: costHistoryChartSection.peakLine.length > 0
                                    text: costHistoryChartSection.peakLine
                                    font: Kirigami.Theme.smallFont
                                    opacity: root.secondaryTextOpacity
                                    Layout.fillWidth: true
                                    elide: Text.ElideRight
                                }

                                Repeater {
                                    model: costHistoryChartSection.rows

                                    delegate: RowLayout {
                                        id: costHistoryMetricRow

                                        Layout.fillWidth: true
                                        spacing: Kirigami.Units.smallSpacing

                                        PlasmaComponents.Label {
                                            id: costHistoryDateLabel

                                            text: modelData.label
                                            font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                            opacity: root.secondaryTextOpacity
                                            Layout.preferredWidth: Kirigami.Units.gridUnit * 5
                                            elide: Text.ElideRight
                                        }

                                        Rectangle {
                                            id: costHistoryBarTrack

                                            Layout.fillWidth: true
                                            Layout.preferredHeight: root.compactMeterTrackHeight
                                            radius: height / 2
                                            color: root.withAlpha(Kirigami.Theme.textColor, 0.055)
                                            clip: true
                                            antialiasing: true

                                            Rectangle {
                                                width: parent.width * Math.max(0, Math.min(100, modelData.percent)) / 100
                                                height: parent.height
                                                radius: parent.radius
                                                antialiasing: true
                                                gradient: Gradient {
                                                    orientation: Gradient.Horizontal

                                                    GradientStop {
                                                        position: 0
                                                        color: root.withAlpha(
                                                            costHistoryChartSection.accent,
                                                            modelData.isPeak ? 0.72 : 0.46)
                                                    }

                                                    GradientStop {
                                                        position: 1
                                                        color: root.withAlpha(
                                                            costHistoryChartSection.accent,
                                                            modelData.isPeak ? 1 : 0.8)
                                                    }
                                                }

                                                Behavior on width {
                                                    NumberAnimation {
                                                        duration: Kirigami.Units.longDuration
                                                        easing.type: Easing.OutCubic
                                                    }
                                                }
                                            }
                                        }

                                        PlasmaComponents.Label {
                                            id: costHistoryValueLabel

                                            text: modelData.value
                                            font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                            opacity: modelData.isPeak ? root.valueTextOpacity : root.secondaryTextOpacity
                                            font.weight: modelData.isPeak ? Font.DemiBold : Font.Normal
                                            horizontalAlignment: Text.AlignRight
                                            Layout.preferredWidth: Kirigami.Units.gridUnit * 8
                                            elide: Text.ElideRight
                                        }
                                    }
                                }
                            }

                            ColumnLayout {
                                id: costDrillDownSection

                                readonly property var breakdownRows: root.costBreakdownRows(tokenCostSection.tokenCost)
                                readonly property var modelRows: root.costModelRows(tokenCostSection.tokenCost)
                                readonly property real metricValueColumnWidth: Kirigami.Units.gridUnit * 9

                                visible: tokenCostSection.tokenCost
                                    && (costDrillDownSection.breakdownRows.length > 0
                                        || costDrillDownSection.modelRows.length > 0)
                                Layout.fillWidth: true
                                spacing: Kirigami.Units.smallSpacing

                                PlasmaComponents.Label {
                                    text: i18n("Cost details")
                                    font.weight: Font.DemiBold
                                    Layout.fillWidth: true
                                    elide: Text.ElideRight
                                }

                                PlasmaComponents.Label {
                                    visible: tokenCostSection.tokenCost && root.costPerMillionLine(tokenCostSection.tokenCost).length > 0
                                    text: tokenCostSection.tokenCost ? root.costPerMillionLine(tokenCostSection.tokenCost) : ""
                                    font: Kirigami.Theme.smallFont
                                    opacity: root.secondaryTextOpacity
                                    Layout.fillWidth: true
                                    elide: Text.ElideRight
                                }

                                ColumnLayout {
                                    visible: costDrillDownSection.breakdownRows.length > 0
                                    Layout.fillWidth: true
                                    spacing: Kirigami.Units.smallSpacing / 2

                                    Repeater {
                                        model: costDrillDownSection.breakdownRows

                                        delegate: RowLayout {
                                            Layout.fillWidth: true
                                            spacing: Kirigami.Units.smallSpacing

                                            PlasmaComponents.Label {
                                                text: modelData.label
                                                font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                                opacity: root.secondaryTextOpacity
                                                Layout.fillWidth: true
                                                elide: Text.ElideRight
                                            }

                                            PlasmaComponents.Label {
                                                id: costBreakdownValueLabel

                                                text: modelData.value
                                                font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                                opacity: root.valueTextOpacity
                                                font.weight: Font.Medium
                                                horizontalAlignment: Text.AlignRight
                                                Layout.preferredWidth: costDrillDownSection.metricValueColumnWidth
                                                Layout.maximumWidth: costDrillDownSection.metricValueColumnWidth
                                                elide: Text.ElideRight
                                            }
                                        }
                                    }
                                }

                                Kirigami.Separator {
                                    visible: costDrillDownSection.modelRows.length > 0
                                    Layout.fillWidth: true
                                    opacity: 0.55
                                }

                                ColumnLayout {
                                    visible: costDrillDownSection.modelRows.length > 0
                                    Layout.fillWidth: true
                                    spacing: Kirigami.Units.smallSpacing / 2

                                    PlasmaComponents.Label {
                                        id: costModelsHeading

                                        text: i18n("Models")
                                        font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                        font.weight: Font.DemiBold
                                        opacity: root.secondaryTextOpacity
                                        Layout.fillWidth: true
                                        elide: Text.ElideRight
                                    }

                                    Repeater {
                                        model: costDrillDownSection.modelRows

                                        delegate: RowLayout {
                                            Layout.fillWidth: true
                                            spacing: Kirigami.Units.smallSpacing

                                            PlasmaComponents.Label {
                                                text: modelData.label
                                                font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                                opacity: root.secondaryTextOpacity
                                                Layout.fillWidth: true
                                                elide: Text.ElideRight
                                            }

                                            PlasmaComponents.Label {
                                                id: costModelValueLabel

                                                text: modelData.value
                                                font.pixelSize: Kirigami.Theme.smallFont.pixelSize
                                                opacity: root.valueTextOpacity
                                                font.weight: Font.Medium
                                                horizontalAlignment: Text.AlignRight
                                                Layout.preferredWidth: costDrillDownSection.metricValueColumnWidth
                                                Layout.maximumWidth: costDrillDownSection.metricValueColumnWidth
                                                elide: Text.ElideRight
                                            }
                                        }
                                    }
                                }

                            }

                            PlasmaComponents.Label {
                                visible: tokenCostSection.tokenCost && tokenCostSection.tokenCost.hintLine.length > 0 ? true : false
                                text: tokenCostSection.tokenCost ? tokenCostSection.tokenCost.hintLine : ""
                                opacity: root.secondaryTextOpacity
                                Layout.fillWidth: true
                                wrapMode: Text.WordWrap
                            }
                        }

                        ColumnLayout {
                            visible: root.selectedProviderData !== null
                            Layout.fillWidth: true
                            spacing: 0

                            Kirigami.Separator {
                                Layout.fillWidth: true
                            }

                            Repeater {
                                id: providerActionRows

                                model: root.selectedProviderData ? root.actionRows(root.selectedProviderData) : []

                                delegate: ColumnLayout {
                                    Layout.fillWidth: true
                                    spacing: 0

                                    Kirigami.Separator {
                                        id: providerActionGroupSeparator

                                        visible: modelData.separatorBefore === true
                                        Layout.fillWidth: true
                                        Layout.topMargin: Kirigami.Units.smallSpacing / 2
                                        Layout.bottomMargin: Kirigami.Units.smallSpacing / 2
                                    }

                                    Controls.ItemDelegate {
                                        Layout.fillWidth: true
                                        text: modelData.title
                                        icon.name: modelData.icon
                                        enabled: modelData.enabled
                                        onClicked: root.performAction(modelData)
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
